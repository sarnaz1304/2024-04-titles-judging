Scruffy Sandstone Loris

high

# FeeManager::_splitProtocolFee sends collectionReferrerShare to wrong address

## Summary

When minting tokens during fee calculation there are 2 different fees for referrers that are calculated, one is for the minting referrer and the other is for the referrer that created the edition (which is stored in `referrers[]`). When calculating the fee they are calcualted seperately passing in the correct 2 different addresses, however when routing the fees, both fees are sent to one referrer. Meaning one referrer receives more fees, whilst the other receives nothing.

## Vulnerability Detail

[FeeManager::_splitProtocolFee()](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L412-L441)
```solidity
    function _splitProtocolFee(
        IEdition edition_,
        address asset_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) internal returns (uint256 referrerShare) {
        // The creation and mint referrers earn 25% and 50% of the protocol's share respectively, if applicable
        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
        uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
        referrerShare = mintReferrerShare + collectionReferrerShare;

        _route(
            Fee({asset: asset_, amount: amount_ - referrerShare}),
            Target({target: protocolFeeReceiver, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: collectionReferrerShare}), // @audit shouldn't the target be referrers[edition_]
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );
    }
```
The referrer fees are calcualted seperated, one set of fees for the minting referred and the other amount for the Editon creation referred:
```solidity
        // The creation and mint referrers earn 25% and 50% of the protocol's share respectively, if applicable
        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
        uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
```

However when the fees are being routed, both sets of fees are sent to `referrer_`:
```solidity
        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: collectionReferrerShare}), 
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );
```
The 2nd route statement above is sending the `collectionReferredShare` to `referrer_` instead of `referrers[edition_]`.

## Impact

The minting referrer will receive the collection referrer's fees, esentially stealing funds from the collection referrer. This will occur on every mint for every Edition's Work.

## Code Snippet

[FeeManager::_splitProtocolFee()](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L412-L441)

## Tool used

Manual Review

## Recommendation

Change `referrer_` to `referrers[edition_]` to correctly send the creation fees:

[FeeManager.sol#L436-L441](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L436-L441)
```diff
        _route(
            Fee({asset: asset_, amount: collectionReferrerShare}), 
-           Target({target: referrer_, chainId: block.chainid}),
+            Target({target: referrers[edition_], chainId: block.chainid}),
            payer_
        );
```