Loud Porcelain Bull

high

# Collection referrers do not receive their revenue share

## Summary

When the feeManager collects the minting fee, the fee for the collection referrer is transferred to the wrong address, resulting in the collection referrer not receiving their revenue share.

## Vulnerability Detail

```solidity
    function _splitProtocolFee(
        IEdition edition_,
        address asset_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) internal returns (uint256 referrerShare) {
        // The creation and mint referrers earn 25% and 50% of the protocol's share respectively, if applicable
        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
        uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
        referrerShare = mintReferrerShare + collectionReferrerShare;

        _route(
            Fee({asset: asset_, amount: amount_ - referrerShare}),
            Target({target: protocolFeeReceiver, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: collectionReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );
    }
```
The `_splitProtocolFee()` function in the `FeeManager` contract handles the fee splitting. The `referrer_` address is the referrer for the minting, while `referrers[edition_]` is the referrer for the collection.
However, seen from the last two _route() call from the above code, the `referrer_` receives not only the `mintReferrerShare` but also the `collectionReferrerShare`, which should be sent to `referrers[edition_]`.

## Impact
Referrers of the collection do not get their rightful share.

## Code Snippet
https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L436-L440

## Tool used

Manual Review

## Recommendation
Sending the `collectionReferrerShare` to the referrer of the collection (i.e. `referrers[edition_]`) in the `_splitProtocolFee` function.
```diff
       _route(
            Fee({asset: asset_, amount: collectionReferrerShare}),
-           Target({target: referrer_, chainId: block.chainid}),
+           Target({target: referrers[edition_], chainId: block.chainid}),
            payer_
        );
```
