Perfect Lipstick Anteater

high

# The `mintBatch` Function in the `Edition` Contract Fails to Mint More Than One Token at a Time

## Summary
The `mintBatch` function in the `Edition` contract is intended to mint multiple tokens simultaneously as per its NatSpec documentation. However, it currently only supports minting one token per transaction due to an issue with how ETH is handled within the function.

## Vulnerability Detail
The `mintBatch` function iterates over the `tokenIds_` array to mint tokens for each ID. Unfortunately, during the first iteration, the function transfers the entire `msg.value` to the `feeManager` contract for fee collection. As a result, when the loop proceeds to the next iteration, there are no remaining funds to mint additional tokens, causing the function to revert.

```javascript
    function mintBatch(
        address to_,
        uint256[] calldata tokenIds_,
        uint256[] calldata amounts_,
        bytes calldata data_
    ) external payable {
        for (uint256 i = 0; i < tokenIds_.length; i++) {
            Work storage work = works[tokenIds_[i]];

            // wake-disable-next-line reentrancy
@>          FEE_MANAGER.collectMintFee{value: msg.value}(
                this, tokenIds_[i], amounts_[i], msg.sender, address(0), work.strategy
            );

            _checkTime(work.opensAt, work.closesAt);
            _updateSupply(work, amounts_[i]);
        }

        _batchMint(to_, tokenIds_, amounts_, data_);
        _refundExcess();
    }
```

## Impact
This limitation prevents users from utilizing the `mintBatch` function to mint multiple tokens in one transaction, which not only defeats the functionâ€™s purpose but also leads to unnecessary gas expenditure and potential user frustration due to transaction reverts.

## Code Snippet
Add the following test to the [`Edition.t.sol`](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/test/editions/Edition.t.sol#L175) file

```javascript
   function test_mintBatch() public {
       uint256[] memory tokenIds = new uint256[](2);
       tokenIds[0] = 1;
       tokenIds[1] = 1;

       uint256[] memory amounts = new uint256[](2);
       amounts[0] = 1;
       amounts[1] = 1;

       // this will revert because of the above mentioned issue
       edition.mintBatch{value: 0.0212 ether}(address(1), tokenIds, amounts, new bytes(0));
       assertEq(edition.totalSupply(1), 2);
       assertEq(address(1).balance, 0.02 ether);
       assertEq(address(0xc0ffee).balance, 0.0012 ether);
   }
```

## Tool used

Manual Review

## Recommendation

Adjust the `mintBatch` function to transfer the appropriate fee for each `tokenId` within the loop rather than the entire `msg.value` upfront. This adjustment ensures that the correct fee is deducted for each token minted, allowing the function to perform as intended. Below is the recommended change:

```diff
    function mintBatch(
        address to_,
        uint256[] calldata tokenIds_,
        uint256[] calldata amounts_,
        bytes calldata data_
    ) external payable {
        for (uint256 i = 0; i < tokenIds_.length; i++) {
            Work storage work = works[tokenIds_[i]];

            // wake-disable-next-line reentrancy
-          FEE_MANAGER.collectMintFee{value: msg.value)}(
+          FEE_MANAGER.collectMintFee{value: mintFee(tokenIds_[i])}(
                this, tokenIds_[i], amounts_[i], msg.sender, address(0), work.strategy
            );

            _checkTime(work.opensAt, work.closesAt);
            _updateSupply(work, amounts_[i]);
        }

        _batchMint(to_, tokenIds_, amounts_, data_);
        _refundExcess();
    }
```
