Breezy Lemonade Shark

medium

# TitlesGraph is incorrectly initialized, breaking upgradeability

## Summary

The TitlesGraph contract, which is intended to be upgradeable, uses an incompatible initialization mechanism that will lead to an incorrect implementation of the upgradeable pattern, breaking contract upgradeability.

## Vulnerability Detail

The TitlesGraph contract inherits from UUPSUpgradeable and is meant to be an upgradeable contract. However, there are two issues in how this is used that will prevent it from being upgradeable.

First, the contract uses a constructor as an initialization mechanism. This is incompatible with an upgradeable contract, since constructors run in the implementation contract, and not in the proxy in front of them. This should be changed to use an initializer.

```solidity
constructor(address owner_, address admin_) {
    _initializeOwner(owner_);
    _grantRoles(admin_, ADMIN_ROLE);
}
```

Second, the TitlesGraph contract is deployed from the TitlesCore contract, but the construction fails to create a ERC1967 proxy in front of it.

```solidity
/// @notice Initializes the protocol.
/// @param feeReceiver_ The address to receive fees.
/// @param splitFactory_ The address of the split factory.
function initialize(address feeReceiver_, address splitFactory_) external initializer {
    _initializeOwner(msg.sender);

    feeManager = new FeeManager(msg.sender, feeReceiver_, splitFactory_);
    graph = new TitlesGraph(address(this), msg.sender);
}
```

This will prevent the TitlesGraph instance from being upgraded, since there is no proxy contract in front of it that could be used to change the implementation contract.

## Proof of Concept

```solidity
function test_graph_cannot_upgrade() public {
    TitlesCore titlesCore = new TitlesCore();
    titlesCore.initialize(address(1), address(new MockSplitFactory()));

    // fetch current instance
    TitlesGraph titlesGraphInstance = titlesCore.graph();

    // Let's suppose we have a new implementation for the TitlesGraph
    address titlesGraphImplementation = address(new TitlesGraph(address(this), address(this)));

    // The upgrade fails
    vm.expectRevert();
    titlesGraphInstance.upgradeToAndCall(titlesGraphImplementation, "");
}
```

## Impact

Contract upgradeability is broken.

## Code Snippet

https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/graph/TitlesGraph.sol#L52-L55

https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/TitlesCore.sol#L47

## Tool used

Manual Review

## Recommendation

First, remove the TitlesGraph constructor in favor of an initializer. 

Second, change the way the TitlesGraph is constructed in the TitlesCore initializer. Either, take a correctly initialized reference as an argument, or initialize the contract as an ERC1967 proxy:

```solidity
contract TitlesCore is OwnableRoles, Initializable, UUPSUpgradeable, Receiver {
    ...
    
    function initialize(address feeReceiver_, address splitFactory_) external initializer {
        _initializeOwner(msg.sender);

        feeManager = new FeeManager(msg.sender, feeReceiver_, splitFactory_);
        
        address graphImplementation = address(new TitlesGraph());
        graph = TitlesGraph(LibClone.deployERC1967(graphImplementation));
        graph.initialize(address(this), msg.sender);
    }
```
