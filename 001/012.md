Attractive Lavender Yeti

medium

# Anyone can create an `Edition` and publish works without paying fees to the `protocolFeeReceiver`

## Summary

Anyone could create a new `Edition` and assign themselves the `EDITION_MANAGER_ROLE`. This allows them to publish new works without paying any fees to the `protocolFeeReceiver` because the `CreationFee` is collected in the `TitlesCore` contract. The published new works can then be added to the `TitleGraph`.

## Vulnerability Detail

The `TitlesCore` contract oversees the creation of `Editions` and the publishing of `works`. Users are required to pay a `CreationFee` to the `protocolFeeReceiver` when they wish to publish new works.

```solidity
    function createEdition(bytes calldata payload_, address referrer_)
        external
        payable
        returns (Edition edition)
    {
        EditionPayload memory payload = abi.decode(payload_.cdDecompress(), (EditionPayload));

        edition = Edition(editionImplementation.clone());

        // wake-disable-next-line reentrancy
        edition.initialize(
            feeManager, graph, payload.work.creator.target, address(this), payload.metadata
        );

        // wake-disable-next-line unchecked-return-value
        _publish(edition, payload.work, referrer_);

        emit EditionCreated(
            address(edition),
            payload.work.creator.target,
            payload.work.maxSupply,
            payload.work.strategy,
            abi.encode(payload.metadata)
        );
    }
```

```solidity
    function _publish(Edition edition_, WorkPayload memory work_, address referrer_)
        internal
        returns (uint256 tokenId)
    {
        // Publish the new Work in the Edition
        // wake-disable-next-line reentrancy
        tokenId = edition_.publish(
            work_.creator.target,
            work_.maxSupply,
            work_.opensAt,
            work_.closesAt,
            work_.attributions,
            work_.strategy,
            work_.metadata
        );
//@audit TitlesCore will collect creation fee when user publishing new works
        // Collect the creation fee
        // wake-disable-next-line reentrancy
        feeManager.collectCreationFee{value: msg.value}(edition_, tokenId, msg.sender);

        // Create the fee route for the new Work
        // wake-disable-next-line reentrancy
        Target memory feeReceiver = feeManager.createRoute(
            edition_, tokenId, _attributionTargets(work_.attributions), referrer_
        );

        // Set the royalty target for the new Work
        // wake-disable-next-line reentrancy
        edition_.setRoyaltyTarget(tokenId, feeReceiver.target);
    }
```

The *CreationFee* can be completely avoided if users create a new `Edition`, assign themselves the `EDITION_MANAGER_ROLE`, and set a self-defined `feemanager`. These user-defined `Editions` and `works` can also be added to the `TitlesGraph`.

```solidity
    function publish(
        address creator_,
        uint256 maxSupply_,
        uint64 opensAt_,
        uint64 closesAt_,
        Node[] calldata attributions_,
        Strategy calldata strategy_,
        Metadata calldata metadata_
    ) external override onlyRoles(EDITION_MANAGER_ROLE) returns (uint256 tokenId) {
        tokenId = ++totalWorks;

        _metadata[tokenId] = metadata_;
        works[tokenId] = Work({
            creator: creator_,
            totalSupply: 0,
            maxSupply: maxSupply_,
            opensAt: opensAt_,
            closesAt: closesAt_,
            strategy: FEE_MANAGER.validateStrategy(strategy_)
        });
//@ the noed is added to the graph
        Node memory _node = node(tokenId);
        for (uint256 i = 0; i < attributions_.length; i++) {
            // wake-disable-next-line reentrancy, unchecked-return-value
            GRAPH.createEdge(_node, attributions_[i], attributions_[i].data);
        }

        emit Published(address(this), tokenId);
    }
```

## Impact

The creator of Eidtion can publish new works without paying fees to the `protocolFeeReceiver`.

## Code Snippet

https://github.com/sherlock-audit/2024-04-titles/blob/c9d16782a7d3c15c7a759f22c9e0552d5e777ed7/wallflower-contract-v2/src/editions/Edition.sol#L103-L131

## Tool used

Manual Review

## Recommendation

Only allow the `Edition` created by `TitleCore` to join the `TitlesGraph`.