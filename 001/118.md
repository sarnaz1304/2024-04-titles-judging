Cheerful Orange Buffalo

high

# The `Edition.mintBatch()` function always reverts when `tokenIds_.length > 1`.

## Summary

The `Edition.mintBatch()` function always reverts when the length of the `tokenIds_` parameter is greater than 1.

## Vulnerability Detail

In the `Edition.mintBatch()` function, if the length of the `tokenIds_` parameter is greater than 1, `FEE_MANAGER.collectMintFee{value: msg.value}` is called more than once. In the second loop, `address(this).balance` becomes 0, causing the transaction to revert.

```solidity
    function mintBatch(
        address to_,
        uint256[] calldata tokenIds_,
        uint256[] calldata amounts_,
        bytes calldata data_
    ) external payable {
        for (uint256 i = 0; i < tokenIds_.length; i++) {
            Work storage work = works[tokenIds_[i]];

            // wake-disable-next-line reentrancy
@>          FEE_MANAGER.collectMintFee{value: msg.value}(
                this, tokenIds_[i], amounts_[i], msg.sender, address(0), work.strategy
            );

            _checkTime(work.opensAt, work.closesAt);
            _updateSupply(work, amounts_[i]);
        }

        _batchMint(to_, tokenIds_, amounts_, data_);
        _refundExcess();
    }
```

## Impact

The `Edition.mintBatch()` function always reverts when the length of the `tokenIds_` parameter is greater than 1.

## Code Snippet

https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L277-L297

## Tool used

Manual Review

## Recommendation

```diff
    function mintBatch(
        address to_,
        uint256[] calldata tokenIds_,
        uint256[] calldata amounts_,
        bytes calldata data_
    ) external payable {
        for (uint256 i = 0; i < tokenIds_.length; i++) {
            Work storage work = works[tokenIds_[i]];

            // wake-disable-next-line reentrancy
-           FEE_MANAGER.collectMintFee{value: msg.value}(
+           FEE_MANAGER.collectMintFee{value: mintFee(tokenIds_[i], amounts_[i])}(
                this, tokenIds_[i], amounts_[i], msg.sender, address(0), work.strategy
            );

            _checkTime(work.opensAt, work.closesAt);
            _updateSupply(work, amounts_[i]);
        }

        _batchMint(to_, tokenIds_, amounts_, data_);
        _refundExcess();
    }
```