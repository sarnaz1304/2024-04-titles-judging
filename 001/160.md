Alert Tweed Ferret

high

# Fees owed to collection referrer are sent to mint referrer instead

## Summary
When protocol fees are split, the fees meant for the collection referrer are sent to the mint referrer instead. Collection referrer will not be able to receive any fees since the mint referrer takes them all.

## Vulnerability Detail
Fee route is created by an admin:

`FeeManager::createRoute` [#L158-159](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L158-L159)
```javascript
    /// @param referrer_ The address of the referrer to receive a share of the fee.
    function createRoute(
        IEdition edition_,
        uint256 tokenId_,
        Target[] calldata attributions_,
        address referrer_
    ) external onlyOwnerOrRoles(ADMIN_ROLE) returns (Target memory receiver) {
        Target memory creator = edition_.node(tokenId_).creator;

        if (attributions_.length == 0) {
            receiver = creator;
        } else {
            (address[] memory targets, uint256[] memory revshares) = _buildSharesAndTargets(
                creator, attributions_, edition_.feeStrategy(tokenId_).revshareBps
            );

            receiver = Target({
                target: splitFactory.createSplit(
                    SplitV2Lib.Split({
                        recipients: targets,
                        allocations: revshares,
                        totalAllocation: 1e6,
                        distributionIncentive: 0
                    }),
                    address(this),
                    creator.target
                    ),
                chainId: creator.chainId
            });
        }

        _feeReceivers[getRouteId(edition_, tokenId_)] = receiver;
        // @audit collection referrer address set here
@>      referrers[edition_] = referrer_;
    }
```

When tokens are minted (i.e, via `Edition::mint`), `FeeManager::collectMintFee` is called, where a call to the internal `_collectMintFee` is made:

`FeeManager::_collectMintFee` [#L407-408](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L407-L408)
```javascript
    function _collectMintFee(
        IEdition edition_,
        uint256 tokenId_,
        uint256 amount_,
        address payer_,
        address referrer_,
        Fee memory fee_
    ) internal {
        .
        .
        .

        // @audit split shares here
        uint256 referrerShare =
            _splitProtocolFee(edition_, fee_.asset, protocolShare, payer_, referrer_);
        emit FeeCollected(address(edition_), tokenId_, fee_.asset, fee_.amount, referrerShare);
    }
```

`FeeManager::_splitProtocolFee` [#L438](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L438)
```javascript
    function _splitProtocolFee(
        IEdition edition_,
        address asset_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) internal returns (uint256 referrerShare) {
        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
        // @audit collection referrer share calculated here
@>      uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
        referrerShare = mintReferrerShare + collectionReferrerShare;

        _route(
            Fee({asset: asset_, amount: amount_ - referrerShare}),
            Target({target: protocolFeeReceiver, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

        // @audit the share is sent to mint referrer instead of collection referrer
        _route(
            Fee({asset: asset_, amount: collectionReferrerShare}),
@>          Target({target: referrer_, chainId: block.chainid}),
            payer_
        );
    }
```

As you can see, target address is set as `referrer_`, which in this case is the mint referrer, instead of the collection referrer. Collection referrer receives 0 fees since it is taken by mint referrer.

`FeeManager::_route`
```javascript
    function _route(Fee memory fee_, Target memory feeReceiver_, address feePayer_) internal {
        if (block.chainid != feeReceiver_.chainId) revert NotRoutable();
        if (fee_.amount == 0) return;

@>      _transfer(fee_.asset, fee_.amount, feePayer_, feeReceiver_.target);
    }
```

`FeeManager::_transfer`
```javascript
    function _transfer(address asset_, uint256 amount_, address from_, address to_) internal {
        if (asset_ == ETH_ADDRESS) {
            to_.safeTransferETH(amount_);
        } else {
            asset_.safeTransferFrom(from_, to_, amount_);
        }
    }
```

## Impact
As mentioned above, collection referrer does not receive owed fees and instead goes to mint referrer.

## Code Snippet
https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L158-L159

https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L407-L408

## Tool used
Manual Review

## Recommendation
Change the target address when routing the `collectionReferrerShare`

```diff
    function _splitProtocolFee(
        IEdition edition_,
        address asset_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) internal returns (uint256 referrerShare) {
        // The creation and mint referrers earn 25% and 50% of the protocol's share respectively, if applicable
        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
        uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
        referrerShare = mintReferrerShare + collectionReferrerShare;

        _route(
            Fee({asset: asset_, amount: amount_ - referrerShare}),
            Target({target: protocolFeeReceiver, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: collectionReferrerShare}),
-           Target({target: referrer_, chainId: block.chainid}),
+           Target({target: referrers[edition_], chainId: block.chainid}),
            payer_
        );
    }
```