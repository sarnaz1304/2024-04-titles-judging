Fun Banana Chameleon

medium

# Edition._refundExcess() never refunds any excess

## Summary

The Edition contract attempts to reimburse users' overpaid eth after a mint by transferring the leftover eth in the contract to `msg.sender` at the end of the call. This is never successful as the total `msg.value` eth has been sent to `FeeManager` before any call to `_refundExcess()`.

This gives a false idea to users that they will be reimbursed after the mint but they will not.

## Vulnerability Detail

The `mint()` function sends `msg.value` along the call `collectMintFee()` to `FEE_MANAGER` before calling `_refundExcess()`:

```solidity
    function mint(
        address to_,
        uint256 tokenId_,
        uint256 amount_,
        address referrer_,
        bytes calldata data_
    ) external payable override {
        // wake-disable-next-line reentrancy
        FEE_MANAGER.collectMintFee{value: msg.value}(
            this, tokenId_, amount_, msg.sender, referrer_, works[tokenId_].strategy
        );

        _issue(to_, tokenId_, amount_, data_);
        _refundExcess();  // @audit does not refund excess since it was sent to FEE_MANAGER
    }
```

`_refundExcess()` supposedly refunds the total balance of the contract (overpaid eth) back to the caller of `mint()`:

```solidity
    /// @notice Refund any excess ETH sent to the contract.
    /// @dev This function is called after minting tokens to refund any ETH left in the contract after all fees have been collected.
    function _refundExcess() internal {
        if (msg.value > 0 && address(this).balance > 0) {
            msg.sender.safeTransferETH(address(this).balance);
        }
    }
```

This will not work as the contract will not be left with any ether from the mint call from `msg.sender`.

## Impact

Users overpaying eth to mint a token will lose the overpaid amount while believing they will be reimbursed.

This is the case for `mint()`, `mintWithComment()` and both `mintBatch()` functions.

## Code Snippet

https://github.com/sherlock-audit/2024-04-titles/blob/d7f60952df22da00b772db5d3a8272a988546089/wallflower-contract-v2/src/editions/Edition.sol#L228-L320

## Tool used

Manual Review

## Recommendation

Calculate the exact fee to be paid to `FeeManager` in `Edition` and only transfer that amount of ether to `FeeManager`.