Fierce Viridian Badger

high

# CollectionReferrerShare is wrongly sent to mint referrer

## Summary
Collection referrer share is sent erroneously to the mint referrer instead of the collection referrer.

## Vulnerability Detail

`_splitProtocolFee` is used to split protocol fee  between `protocolFeeReceiver`,  mint referrer and Edition referrer.
CollectionReferrerShare is erroneously sent to mint referrer. 

https://github.com/sherlock-audit/2024-04-titles/blob/d7f60952df22da00b772db5d3a8272a988546089/wallflower-contract-v2/src/fees/FeeManager.sol#L437-L438

```solidity
    function _splitProtocolFee(
        IEdition edition_,
        address asset_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) internal returns (uint256 referrerShare) {
        // The creation and mint referrers earn 25% and 50% of the protocol's share respectively, if applicable
        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
        uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
        referrerShare = mintReferrerShare + collectionReferrerShare;
        _route(
            Fee({asset: asset_, amount: amount_ - referrerShare}),
            Target({target: protocolFeeReceiver, chainId: block.chainid}),
            payer_
        );
        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );
        _route(
            Fee({asset: asset_, amount: collectionReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),// @audit collectionReferrerShare is sent to *MINT* referrer instead of the collection referrer
            payer_
        );
    }
```

## Impact
Collection referrer gets no reward while mint referrer is paid twice.

## Code Snippet
Provided in Vulnerability Detail.

## Tool used

Manual Review

## Recommendation
Send the share to correct address: 

```solidity
    function _splitProtocolFee(
        IEdition edition_,
        address asset_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) internal returns (uint256 referrerShare) {
...
        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
-            Target({target: referrer_, chainId: block.chainid}),
+            Target({target: referrers[edition_], chainId: block.chainid}),
            payer_
        );
}
```
