Wonderful Ebony Pigeon

high

# Issue with Fee Distribution in `FeeManager.sol` Prevents Collection Referrers from Receiving Fees

## Summary
The `FeeManager.sol#_splitProtocolFee()` function contains a flaw where the `collection referrer share` is incorrectly transferred to the `mint referrer` instead of the `collection referrer`. This results in collection referrers not receiving their fees as intended.
## Vulnerability Detail
In the codebase, when the `mint()` function in `Edition.sol` is called, the fee distribution process involves the `FeeManager.sol#collectMintFee()` function. 
Within this function, the protocol share is distributed using the `_splitProtocolFee()` function.
```solidity
    function _splitProtocolFee(
        IEdition edition_,
        address asset_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) internal returns (uint256 referrerShare) {
        // The creation and mint referrers earn 25% and 50% of the protocol's share respectively, if applicable
        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
        uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
        referrerShare = mintReferrerShare + collectionReferrerShare;

        _route(
            Fee({asset: asset_, amount: amount_ - referrerShare}),
            Target({target: protocolFeeReceiver, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

436:    _route(
437:        Fee({asset: asset_, amount: collectionReferrerShare}),
438:        Target({target: referrer_, chainId: block.chainid}),
438:        payer_
439:    );
    }
```
However, as you can see, the issue arises in the `_splitProtocolFee()` function where the calculated collection referrer share is transferred to the mint referrer instead of the collection referrer. 
This error leads to collection referrers being excluded from receiving their fees.
## Impact
The incorrect implementation of fee distribution in the `_splitProtocolFee()` function results in collection referrers being permanently ineligible to receive their fees.
## Code Snippet
https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L412-L441
## Tool used

Manual Review

## Recommendation
To rectify this issue and ensure collection referrers receive their fees correctly, it is recommended to adjust the `_splitProtocolFee()` function as follows:

```solidity
    function _splitProtocolFee(
        IEdition edition_,
        address asset_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) internal returns (uint256 referrerShare) {
        // The creation and mint referrers earn 25% and 50% of the protocol's share respectively, if applicable
        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
        uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
        referrerShare = mintReferrerShare + collectionReferrerShare;

        _route(
            Fee({asset: asset_, amount: amount_ - referrerShare}),
            Target({target: protocolFeeReceiver, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

---     _route(
---         Fee({asset: asset_, amount: collectionReferrerShare}),
---         Target({target: referrer_, chainId: block.chainid}),
---         payer_
---     );

+++     _route(
+++         Fee({asset: asset_, amount: collectionReferrerShare}),
+++         Target({target: referrers[edition_], chainId: block.chainid}),
+++         payer_
        );
    }
```