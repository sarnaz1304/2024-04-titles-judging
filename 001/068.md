Beautiful Butter Parrot

high

# `Editions::mintBatch` is broken

## Summary
The `mintBatch` function in the `Editions` contract improperly handles ether sent to mint multiple tokens and is broken in its current implementation.

## Vulnerability Detail
The `mintBatch` function enables a caller to mint multiple tokens simultaneously by providing arrays of `tokenIds_` and their corresponding `amounts_`. The function attempts to collect mint fees for each token ID and amounts within a for loop by using msg.value in this call `FEE_MANAGER.collectMintFee{value: msg.value}`. 

This approach to handling ether is flawed and consistently leads to the function reverting when attempting to mint multiple `tokenIds_`. In the for loop, the entire `msg.value` is sent to the `FEE_MANAGER` during the first iteration. Consequently, by the time the second iteration commences, the `msg.value` has been depleted to 0, causing the function to revert. Even if a user were to send the appropriate amount of ether to mint all the tokens, the function would still fail due to this handling error.

## PoC
Add this test to `Editions.t.sol` and run it will `forge test --mt test_mintBatchMsgValueShouldBeCorrect -vvvv` it will fail with EvmError: OutOfFunds even though the appropriate amount of ether is sent to the contract to mint all the tokens in the arrays.

```javascript
    function test_mintBatchMsgValueShouldBeCorrect() public {
        address hacker = makeAddr("hacker");
        vm.deal(hacker, 1 ether);

        uint256[] memory tokenIds = new uint256[](4);
        uint256[] memory amounts = new uint256[](4);
        for (uint256 i = 0; i < 4; i++) {
            tokenIds[i] = 1; // Assuming 1 is the ID for which mintFee applies
            amounts[i] = 1;  // Each ID has an amount of 1
        }
        uint256 amount = 0.0106 ether * 4; 

        vm.startPrank(hacker);
        edition.mintBatch{value: amount}(hacker, tokenIds, amounts, new bytes(0));
        
        vm.assertEq(edition.balanceOf(hacker, 1), 4);   // 4 tokens minted
    }
```

## Impact
`Editions::mintBatch` is broken and users will not be able to batch mint tokens as intended.

## Code Snippet
https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L287

```javascript
 function mintBatch(
        address to_,
        uint256[] calldata tokenIds_,
        uint256[] calldata amounts_,
        bytes calldata data_
    ) external payable {
  for (uint256 i = 0; i < tokenIds_.length; i++) {
      Work storage work = works[tokenIds_[i]];
  
      // wake-disable-next-line reentrancy
      FEE_MANAGER.collectMintFee{value: msg.value}(
          this, tokenIds_[i], amounts_[i], msg.sender, address(0), work.strategy
      );
  
      _checkTime(work.opensAt, work.closesAt);
      _updateSupply(work, amounts_[i]);
}
```

## Tool used

Manual Review
Foundry

## Recommendation
To fix we need to send the appropriate amount of ether in the `FEE_MANAGER.collectMintFee` call. One way to do this is to cache the `msg.value` as a local variable in the function, grab the `mintFee` on each iteration based on the `tokenId` and the `amount`, send that `mintFee` as the value with `FEE_MANAGER.collectMintFee{value: mintFee}`, decrement the cached `msg.value` and continue with the loop checking that there is still enough ether left to send and mint tokens.

```diff
    function mintBatch(
        address to_,
        uint256[] calldata tokenIds_,
        uint256[] calldata amounts_,
        bytes calldata data_
    ) external payable {
    
+      uint256 ethSent = msg.value;

        for (uint256 i = 0; i < tokenIds_.length; i++) {
+            if(ethSent <= 0) {
+                revert("Not enough eth sent to mint tokens");
+             }
       
            Work storage work = works[tokenIds_[i]];
+          uint256 costToMint = mintFee(tokenIds_[i], amounts_[i]);

            // wake-disable-next-line reentrancy
-            FEE_MANAGER.collectMintFee{value: msg.value}(
+           FEE_MANAGER.collectMintFee{value: costToMint}(
                this, tokenIds_[i], amounts_[i], msg.sender, address(0), work.strategy
            );
+         ethSent -= costToMint;
          
            _checkTime(work.opensAt, work.closesAt);
            _updateSupply(work, amounts_[i]);
        }

        _batchMint(to_, tokenIds_, amounts_, data_);
        _refundExcess();
    }
```


After making these changes you will be able to run `forge test --mt test_mintBatchMsgValueShouldBeCorrect -vvvv` again and it will pass.