Fancy Glass Spider

high

# `Edition::mintBatch` is always reverted.

## Summary

`Edition::mintBatch` is reverted at the second loop because there is no money left after the first loop.

## Vulnerability Detail

As seen at `L287` of `Edition::mintBatch`, it sends all ether paid by `msg.sender` to the `FEE_MANAGER`. Therefore, at the second loop, there is no money left, leading to the reversal of the transaction.

```solidity
    function mintBatch(
        address to_,
        uint256[] calldata tokenIds_,
        uint256[] calldata amounts_,
        bytes calldata data_
    ) external payable {
        for (uint256 i = 0; i < tokenIds_.length; i++) {
            Work storage work = works[tokenIds_[i]];

            // wake-disable-next-line reentrancy
287         FEE_MANAGER.collectMintFee{value: msg.value}(
                this, tokenIds_[i], amounts_[i], msg.sender, address(0), work.strategy
            );

            _checkTime(work.opensAt, work.closesAt);
            _updateSupply(work, amounts_[i]);
        }

        _batchMint(to_, tokenIds_, amounts_, data_);
        _refundExcess();
    }
```

## Impact

Selling multiple works to a single buyer simultaneously is not possible.

## Code Snippet

https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L277-L297

## Tool used

Manual Review

## Recommendation

`Edition::mintBatch` should be fixed as follows.

```diff
    function mintBatch(
        address to_,
        uint256[] calldata tokenIds_,
        uint256[] calldata amounts_,
        bytes calldata data_
    ) external payable {
        for (uint256 i = 0; i < tokenIds_.length; i++) {
            Work storage work = works[tokenIds_[i]];

            // wake-disable-next-line reentrancy
-           FEE_MANAGER.collectMintFee{value: msg.value}(
+           FEE_MANAGER.collectMintFee{value: mintFee(tokenIds_[i], amounts_[i])}(
                this, tokenIds_[i], amounts_[i], msg.sender, address(0), work.strategy
            );

            _checkTime(work.opensAt, work.closesAt);
            _updateSupply(work, amounts_[i]);
        }

        _batchMint(to_, tokenIds_, amounts_, data_);
        _refundExcess();
    }
```