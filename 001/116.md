Cheerful Orange Buffalo

high

# The fee distribution in the `FeeManager._splitProtocolFee()` function is incorrect.

## Summary

The `mint fee` is split into several parts, and one part should be given to `collection referrer`. In `FeeManager._splitProtocolFee()`, 
however, the fee intended for the 'collection referrer' is allocated to the 'mint referrer' instead.

## Vulnerability Detail

The following comments outlines the fee distribution for Protocol Share.

    //  - Protocol Share
    //      - If a referred mint, mint referrer gets 50% of the protocol share
    //      - If a referred collection, collection referrer gets 25% of the protcol share
    //      - Protocol fee receiver gets the remainder of the protocol share

Nevertheless, the fee intended for the 'collection referrer' is allocated to the 'mint referrer' instead.

https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L366-L410

```solidity

    function _splitProtocolFee(
        IEdition edition_,
        address asset_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) internal returns (uint256 referrerShare) {
        // The creation and mint referrers earn 25% and 50% of the protocol's share respectively, if applicable
        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
@>      uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
        referrerShare = mintReferrerShare + collectionReferrerShare;

        _route(
            Fee({asset: asset_, amount: amount_ - referrerShare}),
            Target({target: protocolFeeReceiver, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: collectionReferrerShare}),
@>          Target({target: referrer_, chainId: block.chainid}),
            payer_
        );
    }

```

## Impact

`collection referrer` would not receive any fee.

## Code Snippet

https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L366-L410

## Tool used

Manual Review

## Recommendation

```diff

    function _splitProtocolFee(
        IEdition edition_,
        address asset_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) internal returns (uint256 referrerShare) {
        // The creation and mint referrers earn 25% and 50% of the protocol's share respectively, if applicable
        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
        uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
        referrerShare = mintReferrerShare + collectionReferrerShare;

        _route(
            Fee({asset: asset_, amount: amount_ - referrerShare}),
            Target({target: protocolFeeReceiver, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: collectionReferrerShare}),
-            Target({target: referrer_, chainId: block.chainid}),
+            Target({target: referrers[edition_], chainId: block.chainid}),
            payer_
        );
    }

```