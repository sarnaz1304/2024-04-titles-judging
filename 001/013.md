Sunny Gingerbread Dachshund

high

# Wrong collection referrer set in `_splitProtocolFee()` leads to wrong and unhandled token distribution

## Summary
The wrong collection referrer share receiver set inside the function `_splitProtocolFee()` results in wrong token distribution which leads to fund loss for the corresponding referrer.
## Vulnerability Detail
As per [documents](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/docs/src/src/fees/FeeManager.sol/contract.FeeManager.md) and also discussion with the team members in the Discord chat, we can understand that there are two distinct referrers in every work. These referrers are named mint referrer and collection referrer. Each referrer has shares that would be distributed respectively.
According to the documents these shares are defined as:
>mintReferrerRevshareBps
The share of protocol fees to be distributed to the direct referrer of the mint, in basis points (BPS).

`uint16 public mintReferrerRevshareBps = 5000;`
>collectionReferrerRevshareBps
The share of protocol fees to be distributed to the referrer of the collection, in basis points (BPS).

`uint16 public collectionReferrerRevshareBps = 2500;`

We can infer that these two parties are not the same necessarily, especially if there are distinct AI models and separate collection editions. 
The function `_splitProtocolFee()` is responsible for the above-mentioned distribution:

```Solidity
    function _splitProtocolFee(
        IEdition edition_,
        address asset_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) internal returns (uint256 referrerShare) {
        // The creation and mint referrers earn 25% and 50% of the protocol's share respectively, if applicable
        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
        uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
        referrerShare = mintReferrerShare + collectionReferrerShare;

        _route(
            Fee({asset: asset_, amount: amount_ - referrerShare}),
            Target({target: protocolFeeReceiver, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: collectionReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );
    }
```
The `_route()` function is the `_transfer()` function with some checks. So it sends the tokens to the intended receivers.
As it is illustrated, the last two route receivers are identical. It means that all of both `mintReferrerShare` and `collectionReferrerShare` are distributed to the `referrer_` address. But according to the docs, the `collectionReferrerShare` should be distributed to the `referrers[edition_]` which is distinct from the `referrer_`.
In other words, the current implementation states that all of the shares go directly to just one referrer and there wouldn't need to separate the %50 and %25 of the shares which is wrong.
## Impact
Collection referrer share wouldn't reach to its eligible receiver due to the wrong referrer set inside the `_splitProtocolFee()` function.
## Code Snippet
https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L412
https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L438
## Tool used
Manual Review

## Recommendation
Consider changing the code:

```diff
    function _splitProtocolFee(
        IEdition edition_,
        address asset_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) internal returns (uint256 referrerShare) {
        // The creation and mint referrers earn 25% and 50% of the protocol's share respectively, if applicable
        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
        uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
        referrerShare = mintReferrerShare + collectionReferrerShare;

        _route(
            Fee({asset: asset_, amount: amount_ - referrerShare}),
            Target({target: protocolFeeReceiver, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: collectionReferrerShare}),
-           Target({target: referrer_, chainId: block.chainid}),
+           Target({target: referrers[edition_], chainId: block.chainid}),
            payer_
        );
    }
```
