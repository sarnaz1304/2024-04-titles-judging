Dancing Rosewood Copperhead

high

# Due to incorrect implementation of the `FeeManager.sol#_splitProtocolFee()` function, the collection referrer cannot receive fees.

## Summary
In the `FeeManager.sol#_splitProtocolFee()` function, the calculated collection referrer share is incorrectly transferred to the mint referrer. Therefore, the collection referrer cannot receive their fees.
## Vulnerability Detail
When the caller calls the `Edition.sol#mint()` function, the fee is distributed through the `FeeManager.sol#collectMintFee()` function.
```solidity
    function mint(
        address to_,
        uint256 tokenId_,
        uint256 amount_,
        address referrer_,
        bytes calldata data_
    ) external payable override {
        // wake-disable-next-line reentrancy
236:    FEE_MANAGER.collectMintFee{value: msg.value}(
            this, tokenId_, amount_, msg.sender, referrer_, works[tokenId_].strategy
        );

        _issue(to_, tokenId_, amount_, data_);
        _refundExcess();
    }
```
In the `FeeManager.sol#_collectMintFee()` function, `protocol Share` is distributed using the `_splitProtocolFee()` function.
```solidity
    function _collectMintFee(
        IEdition edition_,
        uint256 tokenId_,
        uint256 amount_,
        address payer_,
        address referrer_,
        Fee memory fee_
    ) internal {
        if (fee_.amount == 0) return;

        SNIP...

        _route(
            Fee({asset: fee_.asset, amount: fee_.amount - protocolShare}),
            _feeReceivers[getRouteId(edition_, tokenId_)],
            payer_
        );

407:    uint256 referrerShare =
408:        _splitProtocolFee(edition_, fee_.asset, protocolShare, payer_, referrer_);
        emit FeeCollected(address(edition_), tokenId_, fee_.asset, fee_.amount, referrerShare);
    }
```
However the calculated `collection referrer share` is incorrectly sent to the `mint referrer` rather than to the `collection referrer` in the `_splitProtocolFee()` function.
```solidity
    function _splitProtocolFee(
        IEdition edition_,
        address asset_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) internal returns (uint256 referrerShare) {
        // The creation and mint referrers earn 25% and 50% of the protocol's share respectively, if applicable
        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
        uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
        referrerShare = mintReferrerShare + collectionReferrerShare;

        _route(
            Fee({asset: asset_, amount: amount_ - referrerShare}),
            Target({target: protocolFeeReceiver, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

436:    _route(
437:        Fee({asset: asset_, amount: collectionReferrerShare}),
438:        Target({target: referrer_, chainId: block.chainid}),
438:        payer_
439:    );
    }
```
Here, `referrer_` is the `mint referrer` entered in the `mint()` function.
As a result, Collection referrers are permanently ineligible to receive their fees.
## Impact
Collection referrers are permanently ineligible to receive their fees.
## Code Snippet
https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L412-L441
## Tool used

Manual Review

## Recommendation
Modify the `FeeManager.sol#_splitProtocolFee()` function as follows to transfer the `Collection Referrer Fee` to the `Collection Referrer`.
```solidity
    function _splitProtocolFee(
        IEdition edition_,
        address asset_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) internal returns (uint256 referrerShare) {
        // The creation and mint referrers earn 25% and 50% of the protocol's share respectively, if applicable
        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
        uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
        referrerShare = mintReferrerShare + collectionReferrerShare;

        _route(
            Fee({asset: asset_, amount: amount_ - referrerShare}),
            Target({target: protocolFeeReceiver, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

---     _route(
---         Fee({asset: asset_, amount: collectionReferrerShare}),
---         Target({target: referrer_, chainId: block.chainid}),
---         payer_
---     );

+++     _route(
+++         Fee({asset: asset_, amount: collectionReferrerShare}),
+++         Target({target: referrers[edition_], chainId: block.chainid}),
+++         payer_
        );
    }
```