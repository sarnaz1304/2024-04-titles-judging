Fantastic Cloud Frog

high

# A signature can be used to both unacknowledgeEdge and acknowledgeEdge in TitlesGraph

## Summary
A signature can be used to both unacknowledgeEdge and acknowledgeEdge in TitlesGraph.
## Vulnerability Detail
In TitlesGraph, both unacknowledgeEdge and acknowledgeEdge can be called if we have a valid signature. However, there is no field to record a signature is for acknowledgeEdge or unacknowledgeEdge, which means if a signature for acknowledgeEdge can also be used to unacknowledgeEdge. 
```solidity
  /// @notice Modified to check the signature for a proxied acknowledgment.
  modifier checkSignature(bytes32 edgeId, bytes calldata data, bytes calldata signature) {
      bytes32 digest = _hashTypedData(keccak256(abi.encode(ACK_TYPEHASH, edgeId, data)));
      if (
          !edges[edgeId].to.creator.target.isValidSignatureNowCalldata(digest, signature)
              || _isUsed[keccak256(signature)]
      ) {
          revert Unauthorized();
      }
      _;
      _isUsed[keccak256(signature)] = true;
  }
```

```solidity
  function unacknowledgeEdge(bytes32 edgeId_, bytes calldata data_, bytes calldata signature_)
      external
      checkSignature(edgeId_, data_, signature_)
      returns (Edge memory edge)
  {
      return _setAcknowledged(edgeId_, data_, false);
  }
```

```solidity
  function acknowledgeEdge(bytes32 edgeId_, bytes calldata data_, bytes calldata signature_)
      external
      checkSignature(edgeId_, data_, signature_)
      returns (Edge memory edge)
  {
      return _setAcknowledged(edgeId_, data_, true);
  }
```

## Impact
An attacker can front run to prevent acknowledgeEdge by call unacknowledgeEdge first using same signature. Also, we can apply a signature for unacknowledgeEdge but we can use it to acknowledgeEdge.

## Code Snippet
https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/graph/TitlesGraph.sol#L41
https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/graph/TitlesGraph.sol#L118
https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/graph/TitlesGraph.sol#L146

## Tool used
Manual Review

## Recommendation
Add a field `ack` in Typed Data to indicate whether a signature is for acknowledgeEdge or unacknowledgeEdge.
```solidity
bytes32 digest = _hashTypedData(keccak256(abi.encode(ACK_TYPEHASH, edgeId, data, ack)));
```