Cheerful Orange Buffalo

medium

# An attacker can invalidate signatures for `TitlesGraph.acknowledgeEdge()` and `TitlesGraph.unacknowledgeEdge` by front running.

## Summary

Signature digests do not contain information indicating whether it is for `acknowledgeEdge` or `unacknowledgeEdge`. So, an attacker can invalidate signatures for `acknowledgeEdge()` and `unacknowledgeEdge` by front running.

## Vulnerability Detail

A signature for `acknowledgeEdge()` and `unacknowledgeEdge` is single-use only. And signature digests do not contain information indicating whether it is for `acknowledgeEdge` or `unacknowledgeEdge`.

```solidity

    modifier checkSignature(bytes32 edgeId, bytes calldata data, bytes calldata signature) {
        bytes32 digest = _hashTypedData(keccak256(abi.encode(ACK_TYPEHASH, edgeId, data)));
        if (
            !edges[edgeId].to.creator.target.isValidSignatureNowCalldata(digest, signature)
@>              || _isUsed[keccak256(signature)]
        ) {
            revert Unauthorized();
        }
        _;
@>      _isUsed[keccak256(signature)] = true;
    }

    function acknowledgeEdge(bytes32 edgeId_, bytes calldata data_, bytes calldata signature_)
        external
@>      checkSignature(edgeId_, data_, signature_)
        returns (Edge memory edge)
    {
        return _setAcknowledged(edgeId_, data_, true);
    }

    function unacknowledgeEdge(bytes32 edgeId_, bytes calldata data_, bytes calldata signature_)
        external
@>      checkSignature(edgeId_, data_, signature_)
        returns (Edge memory edge)
    {
        return _setAcknowledged(edgeId_, data_, false);
    }
```

Suppose the following scenario:
1. An attacker is monitoring the transaction pool and discovers a transaction containing `acknowledgeEdge()`.
2. He calls `unacknowledgeEdge()` by using the signature in the above transaction.(front-running)
3. The legitimate `acknowledgeEdge()` will be reverted because the signature is already used in the 2nd step.

## Impact

Signatures for `acknowledgeEdge()` and `unacknowledgeEdge` should be invalidated by front running.

## Code Snippet

https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/graph/TitlesGraph.sol#L40-L50

https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/graph/TitlesGraph.sol#L118-L124

https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/graph/TitlesGraph.sol#L146-L152

## Tool used

Manual Review

## Recommendation

The signature system should be improved.
I recommend that the signature digest should contain information indicating whether it is for 'acknowledgeEdge' or 'unacknowledgeEdge'.

```diff
-   modifier checkSignature(bytes32 edgeId, bytes calldata data, bytes calldata signature) {
-       bytes32 digest = _hashTypedData(keccak256(abi.encode(ACK_TYPEHASH, edgeId, data)));
+   modifier checkSignature(bytes32 edgeId, bytes calldata data, bool isAck, bytes calldata signature) {
+       bytes32 digest = _hashTypedData(keccak256(abi.encode(ACK_TYPEHASH, edgeId, data, isAck)));
        if (
            !edges[edgeId].to.creator.target.isValidSignatureNowCalldata(digest, signature)
                || _isUsed[keccak256(signature)]
        ) {
            revert Unauthorized();
        }
        _;
        _isUsed[keccak256(signature)] = true;
    }

    function acknowledgeEdge(bytes32 edgeId_, bytes calldata data_, bytes calldata signature_)
        external
-       checkSignature(edgeId_, data_, signature_)
+       checkSignature(edgeId_, data_, true, signature_)
        returns (Edge memory edge)
    {
        return _setAcknowledged(edgeId_, data_, true);
    }

    function unacknowledgeEdge(bytes32 edgeId_, bytes calldata data_, bytes calldata signature_)
        external
-       checkSignature(edgeId_, data_, signature_)
+       checkSignature(edgeId_, data_, false, signature_)
        returns (Edge memory edge)
    {
        return _setAcknowledged(edgeId_, data_, false);
    }

```