Scruffy Sandstone Loris

medium

# Edition doesn't update Nodes when Work attributes are changed

## Summary

When Node information is updated, the Graph does not get updated. This can lead to incorrect information being stored in the Graph.

## Vulnerability Detail

[Edition::publish()](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L103-L131)
```solidity
    function publish(
        address creator_,
        uint256 maxSupply_,
        uint64 opensAt_,
        uint64 closesAt_,
        Node[] calldata attributions_,
        Strategy calldata strategy_,
        Metadata calldata metadata_
    ) external override onlyRoles(EDITION_MANAGER_ROLE) returns (uint256 tokenId) {
        tokenId = ++totalWorks;

        _metadata[tokenId] = metadata_;
        works[tokenId] = Work({
            creator: creator_,
            totalSupply: 0,
            maxSupply: maxSupply_,
            opensAt: opensAt_,
            closesAt: closesAt_,
            strategy: FEE_MANAGER.validateStrategy(strategy_)
        });

        Node memory _node = node(tokenId);
        for (uint256 i = 0; i < attributions_.length; i++) {
            // wake-disable-next-line reentrancy, unchecked-return-value
            GRAPH.createEdge(_node, attributions_[i], attributions_[i].data);
        }

        emit Published(address(this), tokenId);
    }
```
When a new Work is published, new Edges in the Graph are created utilising data from `works[tokenId]`, such as the creator being set to `works[tokenId].creator`:

[Edition::node()](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L185-L192)
```solidity
    function node(uint256 tokenId) public view returns (Node memory) {
        return Node({
            nodeType: NodeType.TOKEN_ERC1155,
            creator: Target({target: works[tokenId].creator, chainId: block.chainid}),
            entity: Target({target: address(this), chainId: block.chainid}),
            data: abi.encode(tokenId)
        });
    }
```

However when a Work is transfered the creator is changed without the Edge that was prevously created being updated to specify the new creator.

[Edition::transferWork()](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L412-L420)
```solidity
    function transferWork(address to_, uint256 tokenId_) external {
        Work storage work = works[tokenId_];
        if (msg.sender != work.creator) revert Unauthorized();

        // Transfer the work to the new creator
        work.creator = to_;

        emit WorkTransferred(address(this), tokenId_, to_);
    }
```

## Impact

As the Graph is not updated whenever updates are made to Works, the graph will become outdated over time and inaccurate. Meaning the intended usecase for the Graph of being able to find out all information about Works will not be true.

## Code Snippet

[Edition::publish()](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L103-L131)
[Edition::node()](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L185-L192)
[Edition::transferWork()](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L412-L420)

## Tool used

Manual Review

## Recommendation

Ensure that Edge information is updated whenever Node information is changed. It doesn't seem that [TitlesGraph.sol](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/graph/TitlesGraph.sol) has a way to update Edges, meaning it may be necessary to add new functionality to achieve this.