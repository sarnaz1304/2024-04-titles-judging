Breezy Lemonade Shark

medium

# Invalid initialization of Edition implementation in TitlesCore

## Summary

The TitlesCore contract initializes the implementation of the Edition contract using field declarations, which is incompatible with upgradeable contracts.

## Vulnerability Detail

The TitlesCore contract is meant to be an upgradeable contract, as it inherits from UUPSUpgradeable. Under this schema, the TitlesCore will be a proxy that points to a determined implementation address.

```solidity
contract TitlesCore is OwnableRoles, Initializable, UUPSUpgradeable, Receiver {
    using LibClone for address;
    using LibZip for bytes;
    using SafeTransferLib for address;

    address public editionImplementation = address(new Edition());
    FeeManager public feeManager;
    TitlesGraph public graph;

    /// @notice Initializes the protocol.
    /// @param feeReceiver_ The address to receive fees.
    /// @param splitFactory_ The address of the split factory.
    function initialize(address feeReceiver_, address splitFactory_) external initializer {
        _initializeOwner(msg.sender);

        feeManager = new FeeManager(msg.sender, feeReceiver_, splitFactory_);
        graph = new TitlesGraph(address(this), msg.sender);
    }
```

We can see that `editionImplementation` is initialized using a **field declaration**, as opposed to the other variables that are properly initialized in the `initialize()` function. 

Field declarations are run exactly like the contract's constructor. This means that this variable will be initialized in the implementation contract, instead of the proxy, leading to an uninitialized variable in the main contract.

## Proof of Concept

```solidity
function test_invalid_initialization() public {
    address implementation = address(new TitlesCore());
    TitlesCore titlesCore = TitlesCore(payable(LibClone.deployERC1967(implementation)));
    titlesCore.initialize(makeAddr("feeReceiver"), makeAddr("splitFactory"));

    // edition implementation is missing
    assertEq(titlesCore.editionImplementation(), address(0));

    // try create edition
    TitlesCore.WorkPayload memory workPayload = TitlesCore.WorkPayload({
        creator: Target({target: address(this), chainId: block.chainid}),
        attributions: new Node[](0),
        maxSupply: 100,
        opensAt: uint64(block.timestamp),
        closesAt: uint64(block.timestamp + 3 days),
        strategy: Strategy({
            asset: ETH_ADDRESS,
            mintFee: 0.05 ether,
            revshareBps: 1000,
            royaltyBps: 250
        }),
        metadata: Metadata({label: "Werk", uri: "https://ipfs.io/{{hash}}", data: new bytes(0)})
    });
    Metadata memory metadata = Metadata({label: "Test Edition", uri: "https://ipfs.io/{{hash}}", data: new bytes(0)});

    // creation fails due to missing edition implementation
    vm.expectRevert();
    titlesCore.createEdition(
        LibZip.cdCompress(
            abi.encode(TitlesCore.EditionPayload({work: workPayload, metadata: metadata}))
        ),
        address(0)
    );
}
```

## Impact

Contract functionality is broken as `editionImplementation` will be uninitialized.

## Code Snippet

https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/TitlesCore.sol#L37

## Tool used

Manual Review

## Recommendation

Move the initialization of `editionImplementation` under the `initialize()` function.
