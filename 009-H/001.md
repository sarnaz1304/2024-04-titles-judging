Scruffy Sandstone Loris

medium

# Edition::mintBatch() Uses msg.value on each itteration, meaning after 1st itteration there will not be enough ETH to continue

## Summary

When `Edition::mintBatch()` calls `FEE_MANAGER.collectMintFee` it uses `{value: msg.value}`, however this `msg.value` is utilised in each loop, meaning after the first loop there will be not enough funds to send `msg.value` meaning the transaction will revert. This completely breaks the function, making it unusable in all circumstances (where there is more than 1 itteration, which is the usecase of this function).

## Vulnerability Detail

The function [Edition::mintBatch()](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L277-L297) allows minting for multiple `tokenIds_` within 1 call:

```solidity
    function mintBatch(
        address to_,
        uint256[] calldata tokenIds_,
        uint256[] calldata amounts_,
        bytes calldata data_
    ) external payable {
        for (uint256 i = 0; i < tokenIds_.length; i++) {
            Work storage work = works[tokenIds_[i]];

            // wake-disable-next-line reentrancy
            FEE_MANAGER.collectMintFee{value: msg.value}( 
                this, tokenIds_[i], amounts_[i], msg.sender, address(0), work.strategy 
            );

            _checkTime(work.opensAt, work.closesAt);
            _updateSupply(work, amounts_[i]);
        }

        _batchMint(to_, tokenIds_, amounts_, data_);
        _refundExcess();
    }
```
However `FEE_MANAGER.collectMintFee{value: msg.value}` is used in each loop itteration, meaning that after the first loop there will not be enough funds to send `msg.value` amount of ETH. This will cause the transaction to revert, making this function unusable unless `tokenIds_.length == 1` which defeats the purpose of utilising this function.

## Impact

`Edition::mintBatch()` will always revert when dealing with Native Eth, making the function unusable.

## Code Snippet

[Edition::mintBatch()](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L277-L297)

## Tool used

Manual Review

## Recommendation

Send a proportional amount of Eth for each call:

```diff
    function mintBatch(
        address to_,
        uint256[] calldata tokenIds_,
        uint256[] calldata amounts_,
        bytes calldata data_
    ) external payable {
+      uint256 proportionalValue = msg.value / tokenIds.length;
        for (uint256 i = 0; i < tokenIds_.length; i++) {
            Work storage work = works[tokenIds_[i]];

            // wake-disable-next-line reentrancy
-            FEE_MANAGER.collectMintFee{value: msg.value}(
+            FEE_MANAGER.collectMintFee{value: proportionalValue}(
                this, tokenIds_[i], amounts_[i], msg.sender, address(0), work.strategy
            );
```