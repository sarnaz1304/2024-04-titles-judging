Breezy Lemonade Shark

medium

# `mintBatch()` for multiple token ids is broken

## Summary

The `mintBatch(address, uint256[], uint256[], bytes)` function is inherently broken as it tries to forward all of the received ETH in each call to `collectMintFee()`, breaking functionality when more than one token is minted.

## Vulnerability Detail

One of the variants of the mint function present in the Edition contract is intended to mint multiple token ids in the same call.

```solidity
function mintBatch(
    address to_,
    uint256[] calldata tokenIds_,
    uint256[] calldata amounts_,
    bytes calldata data_
) external payable {
    for (uint256 i = 0; i < tokenIds_.length; i++) {
        Work storage work = works[tokenIds_[i]];

        // wake-disable-next-line reentrancy
        FEE_MANAGER.collectMintFee{value: msg.value}(
            this, tokenIds_[i], amounts_[i], msg.sender, address(0), work.strategy
        );

        _checkTime(work.opensAt, work.closesAt);
        _updateSupply(work, amounts_[i]);
    }

    _batchMint(to_, tokenIds_, amounts_, data_);
    _refundExcess();
}
```

As we can see in the implementation, the function loops through the different `tokenIds` to handle each mint. Inside each iteration, it calls `collectMintFee()` to process the fees associated with each element.

The call to `collectMintFee()` forwards all the sent ETH (`msg.value`) to the FeeManager contract. This means that all iterations of the loop will try to forward all received ETH to the FeeManager. If `msg.value > 0`, the first iteration will succeed, but the second will revert, as the funds are already gone from the Edition contract.

## Proof of Concept

```solidity
function test_mint_batch_fails() public {
    Edition edition = new Edition();
    FeeManager feeManager = new FeeManager(address(0xdeadbeef), address(0xc0ffee), address(new MockSplitFactory()));
    TitlesGraph graph = new TitlesGraph(address(this), address(this));

    edition.initialize(
        feeManager,
        graph,
        address(this),
        address(this),
        Metadata({label: "Test Edition", uri: "ipfs.io/test-edition", data: new bytes(0)})
    );

    // publish two tokens
    uint256 firstTokenId = edition.publish(
        address(1), // creator
        10, // maxSupply
        0, // opensAt
        0, // closesAt
        new Node[](0), // attributions
        Strategy({
            asset: address(0xEeeeeEeeeEeEeeEeEeEeeEEEeeeeEeeeeeeeEEeE),
            mintFee: 0.01 ether,
            revshareBps: 2500, // 25%
            royaltyBps: 250 // 2.5%
        }),
        Metadata({label: "Best Work Ever", uri: "ipfs.io/best-work-ever", data: new bytes(0)})
    );

    // Normally done by the TitlesCore, but we're testing in isolation
    feeManager.createRoute(edition, firstTokenId, new Target[](0), address(0));

    uint256 secondTokenId = edition.publish(
        address(1), // creator
        10, // maxSupply
        0, // opensAt
        0, // closesAt
        new Node[](0), // attributions
        Strategy({
            asset: address(0xEeeeeEeeeEeEeeEeEeEeeEEEeeeeEeeeeeeeEEeE),
            mintFee: 0.01 ether,
            revshareBps: 2500, // 25%
            royaltyBps: 250 // 2.5%
        }),
        Metadata({label: "Best Work Ever", uri: "ipfs.io/best-work-ever", data: new bytes(0)})
    );

    // Normally done by the TitlesCore, but we're testing in isolation
    feeManager.createRoute(edition, secondTokenId, new Target[](0), address(0));

    address minter = makeAddr("minter");

    vm.deal(minter, 1 ether);

    uint256[] memory tokenIds = new uint256[](2);
    uint256[] memory amounts = new uint256[](2);
    tokenIds[0] = firstTokenId;
    amounts[0] = 1;
    tokenIds[1] = secondTokenId;
    amounts[1] = 1;

    // each mint should be 0.0106 ether, hence 0.0106 * 2 = 0.0212
    vm.prank(minter);
    edition.mintBatch{value: 0.0212 ether}(minter, tokenIds, amounts, "");
}
```

## Impact

This variant of `mintBatch()` is broken and will never succeed for the general use case that it is intended for.

## Code Snippet

https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L277-L297

## Tool used

Manual Review

## Recommendation

Either estimate the fees associated to each mint and forward just that amount in each call to `collectMintFee()`, or first forward all ETH to the FeeManager outside of the loop and then just call `collectMintFee()` without sending any value (FeeManager will use the funds already sent to the contract).
