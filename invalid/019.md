Round Frost Panther

high

# `TitlesCore` doesn't track legit `Edition` contracts, allowing the use of malicious `Edition` contracts

## Summary

`TitlesCore` can interact with malicious/modified `Edition` contracts.

## Vulnerability Detail

The `TitlesCore` contract is responsible of the deployment of `Edition` contracts through `createEdition`
and is responsible for the publication of work through the internal function `_publish`.

However, `TitlesCore` can publish work on a modified `Edition` contracts through the `publish` function.

This allows an attacker to create a custom `Edition`-like contract, to bypass any access control or other mechanism set during the legit deployment (through `createEdition`), while still being able to interact with the Titles protocol.

## Impact

As the whole `Edition` logic is controlled by attacker, impacts are multiple:
- HIGH: `FeeManager.createRoute` is only callable from `TitlesCore`, but a route can be created for any contract as `TitlesCore` doesn't ensure this contract is legit or not
- HIGH: Royalty, minting and roles logic in `Edition` can be modified by the attacker (bypassing access control for example)
- HIGH: All ERC-1155 can be stolen from users (by modifying the ERC-1155 logic, or adding a backdoor)


## Code Snippet

The [`TitlesCore.createEdition` doesn't implement any tracking of deployed `Edition`](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/TitlesCore.sol#L72-L96).

```solidity
    function createEdition(bytes calldata payload_, address referrer_)
        external
        payable
        returns (Edition edition)
    {
        EditionPayload memory payload = abi.decode(payload_.cdDecompress(), (EditionPayload));

        edition = Edition(editionImplementation.clone());

        // wake-disable-next-line reentrancy
        edition.initialize(
            feeManager, graph, payload.work.creator.target, address(this), payload.metadata
        );

        // wake-disable-next-line unchecked-return-value
        _publish(edition, payload.work, referrer_);

        emit EditionCreated(
            address(edition),
            payload.work.creator.target,
            payload.work.maxSupply,
            payload.work.strategy,
            abi.encode(payload.metadata)
        );
    }
```

Moreover, the [`TitlesCore.publish` function will check access control by trusting the **external `Edition`](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/TitlesCore.sol#L108).

```solidity
    function publish(Edition edition_, bytes calldata payload_, address referrer_)
        external
        payable
        returns (uint256 tokenId)
    {
        if (!edition_.hasAnyRole(msg.sender, EDITION_PUBLISHER_ROLE)) {// @POC: Trust an external contract, user-controlled
            revert Unauthorized();
        }
        // ...
    }
```

Then, a malicious `Edition` contract can implement a `hasAnyRole` function that always returns `true`, and the `publish` function will be usable.

Finally, [`TitlesCore.publish` will use this malicious `Edition` contract like a legit one](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/TitlesCore.sol#L112) and call the [internal `_publish` function](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/TitlesCore.sol#L120-L148) to:
- publish to this Edition contract
- create a route in FeeManager
- set royalties in Edition

## Tool used

Manual Review

## Recommendation

Consider implementing a tracking of `Edition` contracts that are deployed in a legit way through `createEdition`.

The following patch implements such a fix. Note that this fix may lack an admin function to manually set a trusted `Edition` contract.


```diff
diff --git a/wallflower-contract-v2/src/TitlesCore.sol b/wallflower-contract-v2/src/TitlesCore.sol
index 11f85d9..1605192 100644
--- a/wallflower-contract-v2/src/TitlesCore.sol
+++ b/wallflower-contract-v2/src/TitlesCore.sol
@@ -38,6 +38,8 @@ contract TitlesCore is OwnableRoles, Initializable, UUPSUpgradeable, Receiver {
     FeeManager public feeManager;
     TitlesGraph public graph;
 
+    mapping(Edition => bool) public deployedEditions;
+
     /// @notice Initializes the protocol.
     /// @param feeReceiver_ The address to receive fees.
     /// @param splitFactory_ The address of the split factory.
@@ -78,6 +80,8 @@ contract TitlesCore is OwnableRoles, Initializable, UUPSUpgradeable, Receiver {
 
         edition = Edition(editionImplementation.clone());
 
+        deployedEditions[edition] = true;
+
         // wake-disable-next-line reentrancy
         edition.initialize(
             feeManager, graph, payload.work.creator.target, address(this), payload.metadata
@@ -121,6 +125,7 @@ contract TitlesCore is OwnableRoles, Initializable, UUPSUpgradeable, Receiver {
         internal
         returns (uint256 tokenId)
     {
+        require(deployedEditions[edition_], "Unknwon edition");
         // Publish the new Work in the Edition
         // wake-disable-next-line reentrancy
         tokenId = edition_.publish(
```

*Note: The following patch can be applied through `git apply`.*