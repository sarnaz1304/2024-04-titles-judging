Fierce Viridian Badger

medium

# Edition._refundExcess() doesn't refund the excess ether

## Summary
Any exces ether sent when minting a NFT is not refunded to the caller

## Vulnerability Detail

Protocol implements `_refundExcess` function in order to refund any surplus ether sent when a NFT is minted.

```solidity
    function mint(
        address to_,
        uint256 tokenId_,
        uint256 amount_,
        address referrer_,
        bytes calldata data_
    ) external payable override {
        // wake-disable-next-line reentrancy
        FEE_MANAGER.collectMintFee{value: msg.value}( //@audit forward all the Ether 
            this, tokenId_, amount_, msg.sender, referrer_, works[tokenId_].strategy
        );

        _issue(to_, tokenId_, amount_, data_);
        _refundExcess();// @audit no refund is made
    }

    /// @notice Refund any excess ETH sent to the contract.
    /// @dev This function is called after minting tokens to refund any ETH left in the contract after all fees have been collected.
    function _refundExcess() internal {
        if (msg.value > 0 && address(this).balance > 0) {
            msg.sender.safeTransferETH(address(this).balance);
        }
    }
```

The problem is that all ether sent when any `mint` function is called is forwarded to `FEE_MANAGER` :
`FEE_MANAGER.collectMintFee{value: msg.value}(...)`

 Edition contract hold no ether so no ether is refunded to `msg.sender`. 
 
 Codded PoC: add the following test to Edition.t.sol file:
 ```solidity
    function test_mintAndRefund() public {
        uint256 balanceBefore = address(this).balance;
        // 0.01 ether is sent to Creator(address(1)); 
        // 0.0006 ether is protocol flat fee
        uint256 mintCost = 0.0106 ether;
        edition.mint{value: 1 ether + mintCost}(address(1), 1, 1, address(0), new bytes(0));
        assertEq(edition.totalSupply(1), 1);
        assertEq(address(1).balance, 0.01 ether);
        assertEq(address(0xc0ffee).balance, 0.0006 ether);
        uint256 balanceAfter = address(this).balance;
        // [FAIL. Reason: Excess ether wasn't refunded: 79228162514264337593543950335 != 79228162513264337593543950335]
        assertEq(balanceBefore,  balanceAfter + mintCost, "Excess ether wasn't refunded");
    }
```

## Impact
Excess ether is accumulated in FeeManager contract. 
In best case, protocol must refund the minters manually, spending time and gas fees.

## Code Snippet
https://github.com/sherlock-audit/2024-04-titles/blob/d7f60952df22da00b772db5d3a8272a988546089/wallflower-contract-v2/src/editions/Edition.sol#L236-L241

https://github.com/sherlock-audit/2024-04-titles/blob/d7f60952df22da00b772db5d3a8272a988546089/wallflower-contract-v2/src/editions/Edition.sol#L267

https://github.com/sherlock-audit/2024-04-titles/blob/d7f60952df22da00b772db5d3a8272a988546089/wallflower-contract-v2/src/editions/Edition.sol#L296

https://github.com/sherlock-audit/2024-04-titles/blob/d7f60952df22da00b772db5d3a8272a988546089/wallflower-contract-v2/src/editions/Edition.sol#L319

https://github.com/sherlock-audit/2024-04-titles/blob/d7f60952df22da00b772db5d3a8272a988546089/wallflower-contract-v2/src/editions/Edition.sol#L512-L516

## Tool used

Manual Review

## Recommendation
Implement the refund in FeeManagers.sol `_collectMintFee`
```solidity
    function _collectMintFee(
        IEdition edition_,
        uint256 tokenId_,
        uint256 amount_,
        address payer_,
        address referrer_,
        Fee memory fee_
    ) internal {
        if (fee_.amount == 0) return;


        // For free mints:
        // - Protocol Share = 1/3 of flat fee
        // - Edition Share = 2/3 of flat fee
        //
        // For priced mints:
        // - Protocol Share = 100% of flat fee, shared as follows:
        // - Edition Share = 100% of creator-specified mint cost, 0% of flat fee
        //
        // In both cases, the protocol and edition shares may be split as follows:
        // - Protocol Share
        //   - If a referred mint, mint referrer gets 50% of the protocol share
        //   - If a referred collection, collection referrer gets 25% of the protcol share
        //   - Protocol fee receiver gets the remainder of the protocol share
        // - Edition Share
        //   - Attributions equally split 25% of the edition share, if applicable
        //   - Creator gets the remainder of the edition share


        uint256 protocolFee = protocolFlatFee * amount_;
        uint256 protocolShare;
        if (fee_.amount == protocolFee) {
            protocolShare = protocolFee * protocolFeeshareBps / MAX_BPS;
        } else {
            protocolShare = protocolFee;
        }


        _route(
            Fee({asset: fee_.asset, amount: fee_.amount - protocolShare}),
            _feeReceivers[getRouteId(edition_, tokenId_)],
            payer_
        );


        uint256 referrerShare =
            _splitProtocolFee(edition_, fee_.asset, protocolShare, payer_, referrer_);

+        // refund the ether
+        _route(
+           Fee({asset: fee_.asset, amount: msg.value -  fee_.amount }),
+           payer_,
+            payer_
+        );

        emit FeeCollected(address(edition_), tokenId_, fee_.asset, fee_.amount, referrerShare);
    }
```