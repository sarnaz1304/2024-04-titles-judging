Chilly Lemon Yak

high

# Improper Reference in `FeeManager::_splitProtocolFee` Function

## Summary

The protocol's design specifies that both the mint and collection referrers should earn 50% and 25% of the protocol's fee share individually. However, the implementation in the [FeeManager::_splitProtocolFee](https://github.com/sherlock-audit/2024-04-titles/blob/d7f60952df22da00b772db5d3a8272a988546089/wallflower-contract-v2/src/fees/FeeManager.sol#L412) function erroneously directs the creation referrer's share to the mint referrer instead.

## Vulnerability Detail

The [FeeManager::_splitProtocolFee](https://github.com/sherlock-audit/2024-04-titles/blob/d7f60952df22da00b772db5d3a8272a988546089/wallflower-contract-v2/src/fees/FeeManager.sol#L412) function is designed to distribute fee shares to the collection referrer, mint referrer, and the protocol itself. The current implementation is as follows:

```solidity
FeeManager.sol
412: function _splitProtocolFee(
413:         IEdition edition_,
414:         address asset_,
415:         uint256 amount_,
416:         address payer_,
417:         address referrer_
418:     ) internal returns (uint256 referrerShare) {
419:         // The creation and mint referrers earn 25% and 50% of the protocol's share respectively, if applicable
420:         uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
421:         uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
422:         referrerShare = mintReferrerShare + collectionReferrerShare;
423: 
424:         _route(
425:             Fee({asset: asset_, amount: amount_ - referrerShare}),
426:             Target({target: protocolFeeReceiver, chainId: block.chainid}),
427:             payer_
428:         );
429: 
430:         _route(
431:             Fee({asset: asset_, amount: mintReferrerShare}),
432:             Target({target: referrer_, chainId: block.chainid}),
433:             payer_
434:         );
435: 
436:         _route(
437:             Fee({asset: asset_, amount: collectionReferrerShare}),
438:             Target({target: referrer_, chainId: block.chainid}), //@audit wrong referrer here
439:             payer_
440:         );
441:     }

```

Based on the provided code snippet, the `collectionReferrerShare` should be directed to the collection referrer. However, the implementation currently sends this share to the mint referrer, which is believed to be unintended behavior.

## Impact

The implementation does not allow collection referrers to receive their intended fee share.

## Code Snippet

https://github.com/sherlock-audit/2024-04-titles/blob/d7f60952df22da00b772db5d3a8272a988546089/wallflower-contract-v2/src/fees/FeeManager.sol#L438

## Tool used

Manual Review

## Recommendation

It is recommended to update the implementation of the `FeeManager::_splitProtocolFee` function as follows:

```diff
function _splitProtocolFee(
        IEdition edition_,
        address asset_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) internal returns (uint256 referrerShare) {
        // The creation and mint referrers earn 25% and 50% of the protocol's share respectively, if applicable
        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
        uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
        referrerShare = mintReferrerShare + collectionReferrerShare;

        _route(
            Fee({asset: asset_, amount: amount_ - referrerShare}),
            Target({target: protocolFeeReceiver, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: collectionReferrerShare}),
-           Target({target: referrer_, chainId: block.chainid}),
+           Target({target: referrers[edition_], chainId: block.chainid}),
            payer_
        );
    }
```