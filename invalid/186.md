Alert Tweed Ferret

high

# Excess ETH can be stolen since it is refunded from the incorrect contract

## Summary
When minting tokens from the `Edition` contract, the `FeeManager` contract is called to collect the fees based on the amount of ETH sent by the caller. At the end of the mint call, `Edition::_refundExcess()` is called to refund any excess ETH to the caller. However, ETH was sent to the `FeeManager` contract, which now has the excess, not the `Edition` contract. Therefore the excess will be in `FeeManager`, and excess ETH will not be refunded.

A malicious actor can take advantage of this by minting new tokens and using the excess fees within `FeeManager` to pay for the fees, or taking a portion of the excess by specifying their own address as the referrer upon mint calls.

## Vulnerability Detail

`Edition::mint` [#L228](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L228)
```javascript
    function mint(
        address to_,
        uint256 tokenId_,
        uint256 amount_,
        address referrer_,
        bytes calldata data_
    ) external payable override {
@>      FEE_MANAGER.collectMintFee{value: msg.value}(
            this, tokenId_, amount_, msg.sender, referrer_, works[tokenId_].strategy
        );

        _issue(to_, tokenId_, amount_, data_);
@>      _refundExcess();
    }
```

When this function is called, the `msg.value` is sent to the `FeeManager` contract via `collectMintFee`:

`FeeManager::collectMintFee` [#L366](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L366)
```javascript
    function collectMintFee(
        IEdition edition_,
        uint256 tokenId_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) external payable {
        _collectMintFee(
            edition_, tokenId_, amount_, payer_, referrer_, getMintFee(edition_, tokenId_, amount_)
        );
    }
```    

```javascript
    function _collectMintFee(
        IEdition edition_,
        uint256 tokenId_,
        uint256 amount_,
        address payer_,
        address referrer_,
        Fee memory fee_
    ) internal {
        if (fee_.amount == 0) return;

        .
        .
        .

        uint256 protocolFee = protocolFlatFee * amount_;
        uint256 protocolShare;
        if (fee_.amount == protocolFee) {
            protocolShare = protocolFee * protocolFeeshareBps / MAX_BPS;
        } else {
            protocolShare = protocolFee;
        }
        
        // @audit this call proceeds to transfer ETH to fee receiver
        _route(
            Fee({asset: fee_.asset, amount: fee_.amount - protocolShare}),
            _feeReceivers[getRouteId(edition_, tokenId_)],
            payer_
        );
        
        // @audit this call proceeds to transfer ETH to referrer and protocol
        uint256 referrerShare =
            _splitProtocolFee(edition_, fee_.asset, protocolShare, payer_, referrer_);
        emit FeeCollected(address(edition_), tokenId_, fee_.asset, fee_.amount, referrerShare);
    }
```

Fees are sent to protocol and referrer. Note that an arbitrary `amount_` can be passed in.

At the end of the mint call, an attempt to refund excess ETH to the caller is made:

`Edition::_refundExcess` [#L512](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L512)
```javascript
    function _refundExcess() internal {
        if (msg.value > 0 && address(this).balance > 0) {
            msg.sender.safeTransferETH(address(this).balance);
        }
    }
```

However, the `Edition` contract no longer has excess since the amount `msg.value` was sent to `FeeManager`.

A malicious actor can exploit this the following ways:

1. Call mint and specify excess ETH as `amount_` which will be used to the pay the protocol and referrer fees, effectively paying their fees with other users ETH

2. Call mint and specify their own address as the `referrer`, effectively taking a portion of the excess tokens

## Impact
Excess ETH stolen, allowing the above scenarios.

## Code Snippet
https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L366

https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L412

https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L448-L467

## Tool used
Manual Review

## Recommendation
Perhaps send the excess back to the `Edition` contract from `FeeManager` upon minting.