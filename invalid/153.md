Straight Lilac Peacock

high

# When new Edition token is minted Collection(Edition) referrer doesn't get paid due to logic error

## Summary
When new Edition token is minted collectionReferrer doesn't get his part of the mintFee, because instead of him it goes to the mintReferrer

## Vulnerability Detail
Here you possible scenario:

1. User mints new EditionA token by executing `Edition.sol::mint` which calls `FeeManager.sol::collectMintFee`
2. `FeeManager.sol::collectMintFee` executes `FeeManager.sol::_collectMintFee` which is supposed to collect the mintFee and use `FeeManager.sol::_splitProtocolFee` to allocate the mintFee between protocolFeeReceiver, mintReferrer and collectionReferrer
```solidity
function _splitProtocolFee(
        IEdition edition_,
        address asset_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) internal returns (uint256 referrerShare) {
        // The creation and mint referrers earn 25% and 50% of the protocol's share respectively, if applicable
        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
        uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
        referrerShare = mintReferrerShare + collectionReferrerShare;

        _route(
            Fee({asset: asset_, amount: amount_ - referrerShare}),
            Target({target: protocolFeeReceiver, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: collectionReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );
    }
```
3. Instead of this the function `FeeManager.sol::_splitProtocolFee` splits the mintFee between protocolFeeReceiver and mintReferrer only

Note that `collectionReferrerShare` and `mintReferrerShare` are routed to referrer_(mintReferrer)
```solidity
function _splitProtocolFee(

       ...

        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
        uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);

       ...

         _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: collectionReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );
    }

```

## Impact
collectionReferrer doesn't receive any part from the mintFee while collectionReferrer's part goes to mintReferrer.

## Code Snippet
https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L412-L441

## Tool used

Manual Review

## Recommendation
Modify `FeeManager.sol::_splitProtocolFee` as shown below:
```diff
function _splitProtocolFee(
        IEdition edition_,
        address asset_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) internal returns (uint256 referrerShare) {
        // The creation and mint referrers earn 25% and 50% of the protocol's share respectively, if applicable
        uint256 mintReferrerShare = getMintReferrerShare(amount_, referrer_);
        uint256 collectionReferrerShare = getCollectionReferrerShare(amount_, referrers[edition_]);
        referrerShare = mintReferrerShare + collectionReferrerShare;

        _route(
            Fee({asset: asset_, amount: amount_ - referrerShare}),
            Target({target: protocolFeeReceiver, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: mintReferrerShare}),
            Target({target: referrer_, chainId: block.chainid}),
            payer_
        );

        _route(
            Fee({asset: asset_, amount: collectionReferrerShare}),
-           Target({target: referrer_, chainId: block.chainid}),
+           Target({target: referrers[edition_], chainId: block.chainid}),
            payer_
        );
    }
```
