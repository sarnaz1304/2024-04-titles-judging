Round Frost Panther

high

# Creation and Minting fees can be bypassed when fees should be paid in ETH

## Summary

Users can bypass minting and creation fees payment by using the FeeManager ETH balance.

## Vulnerability Detail

During the collect of minting and creation fees, there can be two scenarios:
- Fees should be paid with an ERC20 token
- Fees should be paid with the native token (ETH)

When fees should be paid with the native token, there are no checks to ensure that the `msg.value` is equal to the expected minting fees amount.

A user can pass `msg.value = 0` while expected `fee.amount = 1 ether`. Then, the `FeeManager` ETH balance will be used for minting and creation fees payment.

*Note: It is likely to happen as no excessive amount refund mechanism exist in `FeeManager`.*

## Impact

User can bypass creation fees and minting fees payment, to avoid paying protocol, creators and attributions.

## Code Snippet

The [`FeeManager._collectMintFee` function doesn't ensure](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L366-L410) that the provided minting fee matches `msg.value` before calling `_route`. [`FeeManager.collectCreationFee` function doesn't ensure](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L166-L175) doesn't check it neither.

None of [`_route` function](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L448-L454) or [`_transfer` function](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L461-L467) do this `msg.value` check.


```solidity
    function collectCreationFee(IEdition edition_, uint256 tokenId_, address feePayer_)
        external
        payable
    {
        Fee memory fee = getCreationFee();
        if (fee.amount == 0) return;

        _route(fee, Target({target: protocolFeeReceiver, chainId: block.chainid}), feePayer_);
        emit FeeCollected(address(edition_), tokenId_, ETH_ADDRESS, fee.amount, 0);
    }

    function _collectMintFee(
        IEdition edition_,
        uint256 tokenId_,
        uint256 amount_,
        address payer_,
        address referrer_,
        Fee memory fee_
    ) internal {
        if (fee_.amount == 0) return;

        // ...

        uint256 protocolFee = protocolFlatFee * amount_;
        uint256 protocolShare;
        if (fee_.amount == protocolFee) {
            protocolShare = protocolFee * protocolFeeshareBps / MAX_BPS;
        } else {
            protocolShare = protocolFee;
        }

        _route(
            Fee({asset: fee_.asset, amount: fee_.amount - protocolShare}),
            _feeReceivers[getRouteId(edition_, tokenId_)],
            payer_
        );

        uint256 referrerShare =
            _splitProtocolFee(edition_, fee_.asset, protocolShare, payer_, referrer_);
        emit FeeCollected(address(edition_), tokenId_, fee_.asset, fee_.amount, referrerShare);
    }

    function _route(Fee memory fee_, Target memory feeReceiver_, address feePayer_) internal {
        // Cross-chain fee routing is not supported yet
        if (block.chainid != feeReceiver_.chainId) revert NotRoutable();
        if (fee_.amount == 0) return;

        _transfer(fee_.asset, fee_.amount, feePayer_, feeReceiver_.target);
    }

    function _transfer(address asset_, uint256 amount_, address from_, address to_) internal {
        if (asset_ == ETH_ADDRESS) {
            to_.safeTransferETH(amount_);
        } else {
            asset_.safeTransferFrom(from_, to_, amount_);
        }
    }
```

Then, if `msg.value < fee.amount`, the ETH balance of FeeManager will be used and users will not pay for minting fees.

## Tool used

Manual Review

## Recommendation

Consider adding a `msg.value` check to ensure that it matches the expected fee amount.

This can be done by modifying `_transfer`:
```solidity
    function _transfer(address asset_, uint256 amount_, address from_, address to_) internal {
        if (asset_ == ETH_ADDRESS) {
            require(msg.value == amount_, "unexpected value");
            to_.safeTransferETH(amount_);
        } else {
            asset_.safeTransferFrom(from_, to_, amount_);
        }
    }
```