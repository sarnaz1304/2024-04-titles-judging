Precise Carmine Carp

medium

# `Edition.mintBatch()` will revert for batches with more than one tokens

## Summary
[Edition.mintBatch()](https://github.com/sherlock-audit/2024-04-titles/blob/d7f60952df22da00b772db5d3a8272a988546089/wallflower-contract-v2/src/editions/Edition.sol#L277-L297) is uses to mint a batch of tokens, but when paying for fees, it forwards the `msg.value` to the `FeeManager` in a for loop. After the first iteration, the contract will have no more funds and the second iteration will revert.

## Vulnerability Detail
The `mintBatch` function runs a loop for each token it has to mint. When it calls `FEE_MANAGER.collectMintFee()`, the whole `msg.value` is being sent. This will leave the contract with no funds to send for the next iteration and the 
```solidity
        for (uint256 i = 0; i < tokenIds_.length; i++) {
            Work storage work = works[tokenIds_[i]];

            // wake-disable-next-line reentrancy
            FEE_MANAGER.collectMintFee{value: msg.value}(
                this, tokenIds_[i], amounts_[i], msg.sender, address(0), work.strategy
            );

            _checkTime(work.opensAt, work.closesAt);
            _updateSupply(work, amounts_[i]);
        }

```
## Impact
Batch minting with this function is impossible.

## Code Snippet
A foundry PoC to be run in [Edition.t.sol](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/test/editions/Edition.t.sol)
```solidity
 function testFail_mintBatch() public {
        edition.publish(
            address(1), // creator
            10, // maxSupply
            0, // opensAt
            0, // closesAt
            new Node[](0), // attributions
            Strategy({
                asset: address(0xEeeeeEeeeEeEeeEeEeEeeEEEeeeeEeeeeeeeEEeE),
                mintFee: 0.01 ether,
                revshareBps: 2500, // 25%
                royaltyBps: 250 // 2.5%
            }),
            Metadata({label: "Best Work Ever", uri: "ipfs.io/best-work-ever", data: new bytes(0)})
        );

        // Normally done by the TitlesCore, but we're testing in isolation
        feeManager.createRoute(edition, 2, new Target[](0), address(0));

        uint256[] memory ids = new uint256[](2);
        uint256[] memory amounts = new uint256[](2);

        ids[0] = 1;
        ids[1] = 2;
        amounts[0] = 1;
        amounts[1] = 1;

        edition.mintBatch{value: 10 ether}(address(1), ids, amounts, new bytes(0));
        assertEq(edition.totalSupply(1), 1);
        assertEq(edition.totalSupply(2), 1);
    }
```

## Tool used

Foundry

## Recommendation
Send the ETH before the loop.
```solidity
        (bool feeSent, ) = address(FEE_MANAGER).call{value: msg.value}("");
        require(feeSent);
            
        for (uint256 i = 0; i < tokenIds_.length; i++) {
            Work storage work = works[tokenIds_[i]];

            // wake-disable-next-line reentrancy
            FEE_MANAGER.collectMintFee(
                this, tokenIds_[i], amounts_[i], msg.sender, address(0), work.strategy
            );

            _checkTime(work.opensAt, work.closesAt);
            _updateSupply(work, amounts_[i]);
        }
```