Scruffy Sandstone Loris

high

# Edition:mintBatch() collects minted fees once, but it should collect fees per mint

## Summary

When minting a batch of tokens through `Edition:mintBatch()` the fees are only calculated once, however they should be calculated per `issue` or per receiver. This results in less fees for the protocol, and will occur on every call to `mintBatch()` where there is more than 1 receiver.

## Vulnerability Detail

When minting multiple lots of `tokenId_` `amount_`s in [Edition::mintBatch](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L304-L320) `FEE_MANAGER.collectMintFee` is only called once with `amount_`. However the function is minting 
`receivers_.length * amount_` amount of tokens:

[Edition::mintBatch()](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L304-L320)
```solidity
    function mintBatch(
        address[] calldata receivers_,
        uint256 tokenId_,
        uint256 amount_,
        bytes calldata data_
    ) external payable {
        // wake-disable-next-line reentrancy
        FEE_MANAGER.collectMintFee{value: msg.value}(
            this, tokenId_, amount_, msg.sender, address(0), works[tokenId_].strategy
        );

        for (uint256 i = 0; i < receivers_.length; i++) {
            _issue(receivers_[i], tokenId_, amount_, data_);
        }

        _refundExcess();
    }
```

The protocol receives their flat fee based on how many tokens are being minted:

[FeeManager.sol#L393-L399](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L393-L399)
```solidity
        uint256 protocolFee = protocolFlatFee * amount_;
        uint256 protocolShare;
        if (fee_.amount == protocolFee) {
            protocolShare = protocolFee * protocolFeeshareBps / MAX_BPS;
        } else {
            protocolShare = protocolFee;
        }
```
In the current code they will only receive their flat fee for the first receiver's mint, the other receivers' amounts will not be considered in the minting fee.

## Impact

The incorrect fee collection results in less fees for the protocol, which will occur on every single `mintBatch()` call.

## Code Snippet

[Edition::mintBatch()](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/editions/Edition.sol#L304-L320)
[FeeManager.sol#L393-L399](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/fees/FeeManager.sol#L393-L399)

## Tool used

Manual Review

## Recommendation

Multiply `amount_` by the number of receivers to calculate the appropriate amount of tokens that is being minted:
```diff
    function mintBatch(
        address[] calldata receivers_,
        uint256 tokenId_,
        uint256 amount_,
        bytes calldata data_
    ) external payable {
+      uint256 totalAmount = receivers_.length * amount_;
        // wake-disable-next-line reentrancy
        FEE_MANAGER.collectMintFee{value: msg.value}(
-           this, tokenId_, amount_, msg.sender, address(0), works[tokenId_].strategy
+          this, tokenId_, totalAmount, msg.sender, address(0), works[tokenId_].strategy
        );
```
