Feisty Burgundy Wren

high

# _setAcknowledged is not implemented correctly

## Summary
_setAcknowledged is not implemented correctly and due it, the edges/references won't be acknowledged.
## Vulnerability Detail
Alice decides to acknowledge a work, so she calls `acknowledgeEdge`:
```solidity
function acknowledgeEdge(bytes32 edgeId_, bytes calldata data_)
        external
        override
        returns (Edge memory edge)
    {
        if (!_isCreatorOrEntity(edges[edgeId_].to, msg.sender)) revert Unauthorized();
        return _setAcknowledged(edgeId_, data_, true);
    }
```
it first authorizes if the Alice is the referenced person or not and then it calls `_setAcknowledged`:
```solidity
function _setAcknowledged(bytes32 edgeId_, bytes calldata data_, bool acknowledged_)
        internal
@>        returns (Edge memory edge)
    {
        if (!_edgeIds.contains(edgeId_)) revert NotFound();
@>       edge = edges[edgeId_];
@>       edge.acknowledged = acknowledged_;

        if (acknowledged_) {
            emit EdgeAcknowledged(edge, msg.sender, data_);
        } else {
            emit EdgeUnacknowledged(edge, msg.sender, data_);
        }
    }
```
This function is trying to update/set the `acknowledged` flag to `true`, but actually it is not.
As you see in the function, the `edge` variable is `memory` variable, when we come to this line:
```solidity
edge = edges[edgeId_];
```
we are caching the edges[edgeId_] into the memory, and when it come to this line:
```solidity
edge.acknowledged = acknowledged_;
```
it changes the `acknowledged` flag in the memory variable but not in storage and the `edges` mapping will remain unchanged and won't be changed.

## Impact
Users are unable to acknowledge the references.
## Code Snippet
https://github.com/sherlock-audit/2024-04-titles/blob/c9d16782a7d3c15c7a759f22c9e0552d5e777ed7/wallflower-contract-v2/src/graph/TitlesGraph.sol#L103-L110

https://github.com/sherlock-audit/2024-04-titles/blob/c9d16782a7d3c15c7a759f22c9e0552d5e777ed7/wallflower-contract-v2/src/graph/TitlesGraph.sol#L200-L206
## Tool used

Manual Review

## Recommendation
```diff
-       edge = edges[edgeId_];
-       edge.acknowledged = acknowledged_;
+      edges[edgeId_].acknowledged = acknowledged_;
+      edge = edges[edgeId_];
```