Wobbly Lilac Sparrow

medium

# `TitlesGraph::_setAcknowledged` does not properly update the `acknowledged` flag

## Summary

`TitlesGraph::_setAcknowledged` does not properly update the `acknowledged` flag of the Edge struct

## Vulnerability Detail

When acknowledging an `Edge` via `TitlesGraph::_setAcknowledged`, the `acknowledged` flag is not properly updated, due to the fact that the function is updating the return value of type `Edge memory`, changes to which is not persisted in storage.

## Impact

Failed update would violate the sanctity of the data in the `TitlesGraph`, leading to potential future issues if the `acknowledged` flag was to be leveraged to validate `Edge` traversal.

## Code Snippet

[TitlesGraph.sol#L202](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/graph/TitlesGraph.sol#L202)

## Tool used

Manual Review, Foundry

<details>
<summary>Proof of Code</summary>
Add the following code to the `TitlesGraph.t.sol` file:

```javascript
function test_acknowledgeEdge_doesntUpdateStorage() public {
    // ********** Setup **********
    Node memory from = Node({
        nodeType: NodeType.COLLECTION_ERC1155,
        entity: Target({target: address(1), chainId: block.chainid}),
        creator: Target({target: address(2), chainId: block.chainid}),
        data: ""
    });

    Node memory to = Node({
        nodeType: NodeType.TOKEN_ERC1155,
        entity: Target({target: address(3), chainId: block.chainid}),
        creator: Target({target: address(4), chainId: block.chainid}),
        data: abi.encode(42)
    });

    // Only the `from` node's entity can create the edge.
    vm.prank(from.entity.target);
    titlesGraph.createEdge(from, to, "");

    vm.expectEmit(true, true, true, true);
    emit IEdgeManager.EdgeAcknowledged(
        Edge({from: from, to: to, acknowledged: true, data: ""}), to.creator.target, ""
    );
    // ***************************

    // ********** Acknowledge the edge **********
    // Only the `to` node's creator (or the entity itself) can acknowledge it
    bytes32 _edgeId = keccak256(abi.encode(from, to));

    vm.prank(to.creator.target);
    titlesGraph.acknowledgeEdge(_edgeId, "");
    // ******************************************

    // *** Confirm that the update didn't go through ***
    (,, bool acknowledged,) = titlesGraph.edges(_edgeId);
    assertFalse(acknowledged);
}
```
</details>

## Recommendation

Change the type of the return value to Edge storage, so that it would properly update the storage variable.

```diff
function _setAcknowledged(bytes32 edgeId_, bytes calldata data_, bool acknowledged_)
        internal
-       returns (Edge memory edge)        
+       returns (Edge storage edge)
{
    // ...
}
```