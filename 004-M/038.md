Lively Rose Horse

medium

# [M-1] `TitlesGraph::_setAcknowledged` does not change the state variable correctly

## Summary  
the `_setAcknowledged` function is an internal function which is called to change the `edge.acknowledged` bool in a certain edge given its `edgeId`, but instead of changing the `edges` mapping which is a state variable it changes the memory struct `edge`.

## Vulnerability Detail
The _setAcknowledged function is designed to modify the acknowledged status of an Edge object within the edges mapping. However, the function only modifies a local memory copy of the Edge object, rather than updating the state variable directly. This means that the acknowledged status is not persisted across transactions, causing all edges to appear unacknowledged and incapable of being changed. 
[Link to Code](https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/graph/TitlesGraph.sol#L206
)
```javascript
function _setAcknowledged(
        bytes32 edgeId_,
        bytes calldata data_,
        bool acknowledged_
    ) internal returns (Edge memory edge) {
        if (!_edgeIds.contains(edgeId_)) revert NotFound();
@>      edge = edges[edgeId_];
@>      edge.acknowledged = acknowledged_; 

        if (acknowledged_) {
            emit EdgeAcknowledged(edge, msg.sender, data_);
        } else {
            emit EdgeUnacknowledged(edge, msg.sender, data_);
        }
    }
```
## Impact
This bug impacts the functionality of the protocol, as it prevents the intended acknowledgment of edges. Consequently, all edges created within the protocol will remain unacknowledged, rendering them useless for their intended purposes. This issue could potentially lead to a halt in the protocol's operations, as the ability to acknowledge edges is a fundamental requirement for its functionality.

## Code Snippet
to prove this concept just add `isAcknowledged` view function to `TitlesGraph.sol`, this function retruns the acknowledged bool for a given edgeId:
<details>
<summary> isAcknowledged view function:</summary>

```javascript
contract TitlesGraph is
    IOpenGraph,
    IEdgeManager,
    OwnableRoles,
    EIP712,
    UUPSUpgradeable
{
    .
    .
    .
    //my functions
    function isAcknowledged(bytes32 edgeId_) external view returns (bool) {
        Edge memory edge;
        edge = edges[edgeId_];
        bool acknowledged = edge.acknowledged;
        return acknowledged;
    }

}

```
</details>


And make these changes to already exisiting test suits:`test_acknowledgeEdge_withSignature` and `test_acknowledgeEdge` in `TitlesGraph.t.sol`:
<details>
<summary> Test suit changes:</summary>

```diff
function test_acknowledgeEdge() public {
        Node memory from = Node({
            nodeType: NodeType.COLLECTION_ERC1155,
            entity: Target({target: address(1), chainId: block.chainid}),
            creator: Target({target: address(2), chainId: block.chainid}),
            data: ""
        });

        Node memory to = Node({
            nodeType: NodeType.TOKEN_ERC1155,
            entity: Target({target: address(3), chainId: block.chainid}),
            creator: Target({target: address(4), chainId: block.chainid}),
            data: abi.encode(42)
        });

        // Only the `from` node's entity can create the edge.
        vm.prank(from.entity.target);
        titlesGraph.createEdge(from, to, "");

        vm.expectEmit(true, true, true, true);
        emit IEdgeManager.EdgeAcknowledged(
            Edge({from: from, to: to, acknowledged: true, data: ""}),
            to.creator.target,
            ""
        );

        // Only the `to` node's creator (or the entity itself) can acknowledge it
        vm.prank(to.creator.target);
        titlesGraph.acknowledgeEdge(keccak256(abi.encode(from, to)), "");
+       bytes32 edgeId = titlesGraph.getEdgeId(from, to);
+       assertEq(titlesGraph.isAcknowledged(edgeId), false);
    }

    function test_acknowledgeEdge_withSignature() public {
        .
        .
        .
        // A different signature will acknowledge the edge again
        vm.expectEmit(true, true, true, true);
        emit IEdgeManager.EdgeAcknowledged(edge, address(this), "");
        titlesGraph.acknowledgeEdge(edgeId, new bytes(0), signer.SRSLY());

+       assertEq(titlesGraph.isAcknowledged(edgeId), false);
    }
```

</details>

run these tests using command `forge test --mt test_acknowledgeEdge`, both tests pass meaning the acknowledged did not change, the same thing can be done for `unacknowledgeEdge` function tests.

## Tool used

Manual Review, Foundry test suit

## Recommendation
To fix this just change the state var in the `_setAcknowledged` function:
```diff
    function _setAcknowledged(
        bytes32 edgeId_,
        bytes calldata data_,
        bool acknowledged_
    ) internal returns (Edge memory edge) {
        if (!_edgeIds.contains(edgeId_)) revert NotFound();
        edge = edges[edgeId_];
-       edge.acknowledged = acknowledged_; //@audit this sets edge(memory) to acknowledged but not the storage one?
+       edges[edgeId_].acknowledged = acknowledged_;
        if (acknowledged_) {
            emit EdgeAcknowledged(edge, msg.sender, data_);
        } else {
            emit EdgeUnacknowledged(edge, msg.sender, data_);
        }
    }
```
