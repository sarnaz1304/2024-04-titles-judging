Unique Seafoam Tadpole

high

# Possible signature attack in TitlesGraph::acknowledgeEdge()/unacknowledgeEdge()

## Summary
Signature in acknowledgeEdge()/unacknowledgeEdge() is hash for edgeId & data, not including is acknowledgeEdge or not. The same signature can be used to `acknowledgeEdge` one edge or `unacknowledgeEdge` one edge.

## Vulnerability Detail
In TitlesGraph, someone can acknowledge or unacknowledge one edge via one signature. However, this signature is generated by hash-value of `edgeId` and `data`. It does not include the specific information whether this is one acknowledge or unacknowledged signature.   
```c
    modifier checkSignature(bytes32 edgeId, bytes calldata data, bytes calldata signature) {
        bytes32 digest = _hashTypedData(keccak256(abi.encode(ACK_TYPEHASH, edgeId, data)));
        if (
            !edges[edgeId].to.creator.target.isValidSignatureNowCalldata(digest, signature)
                || _isUsed[keccak256(signature)]
        ) {
            revert Unauthorized();
        }
        _;
        _isUsed[keccak256(signature)] = true;
    }
    function acknowledgeEdge(bytes32 edgeId_, bytes calldata data_, bytes calldata signature_)
        external
        checkSignature(edgeId_, data_, signature_)
        returns (Edge memory edge)
    {
        return _setAcknowledged(edgeId_, data_, true);
    }
    function unacknowledgeEdge(bytes32 edgeId_, bytes calldata data_, bytes calldata signature_)
        external
        checkSignature(edgeId_, data_, signature_)
        returns (Edge memory edge)
    {
        return _setAcknowledged(edgeId_, data_, false);
    }
```
According to the vulnerability, one possible attack vector might be like as below:
- Alice signs one signature to acknowledge one edge, eg edgeId = 10.
- Bob monitors this from mempool, and unacknowledgeEdge the same edge via frontrun. The edge will be unacknowledged.
- Alice's acknowledge will be reverted, because the same signature has been used in TitlesGraph.

## Impact
Hacker can make use of signature in acknowledgeEdge()/unacknowledgeEdge() to modify acknowledge result.

## Code Snippet
https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/graph/TitlesGraph.sol#L118-L124
https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/graph/TitlesGraph.sol#L146-L152
## Tool used

Manual Review

## Recommendation
Add one boolean value `isAcknowledgeEdge` into signature generation. In this case, hackers cannot make use of users' signature to do other things that users are not expected.
