Loud Porcelain Bull

high

# The signature for `TitlesGraph.acknowledgeEdge()` can be used in `TitlesGraph.unacknowledgeEdge()` and vice versa

## Summary

The `acknowledgeEdge()` and `unacknowledgeEdge()` functions in the `TitlesGraph` contracts share the same `checkSignature()` logic. This means the signature for `acknowledgeEdge()` can be used in `unacknowledgeEdge()` and vice versa. 
As a result, malicious attackers can prevent the signature from being correctly used. Moreover, malicious relayers can save the signature for later use to mess the edge's acknowledged state.

## Vulnerability Detail
```solidity
    function acknowledgeEdge(bytes32 edgeId_, bytes calldata data_, bytes calldata signature_)
        external
        checkSignature(edgeId_, data_, signature_)
        returns (Edge memory edge)
    {
        return _setAcknowledged(edgeId_, data_, true);
    }
```
```solidity
    function unacknowledgeEdge(bytes32 edgeId_, bytes calldata data_, bytes calldata signature_)
        external
        checkSignature(edgeId_, data_, signature_)
        returns (Edge memory edge)
    {
        return _setAcknowledged(edgeId_, data_, false);
    }
```
The `acknowledgeEdge()` and `unacknowledgeEdge()` functions in the `TitlesGraph` contracts share the same `checkSignature()` modifier. This means the signature for `acknowledgeEdge()` can be used in `unacknowledgeEdge()` and vice versa.

```solidity
    modifier checkSignature(bytes32 edgeId, bytes calldata data, bytes calldata signature) {
        bytes32 digest = _hashTypedData(keccak256(abi.encode(ACK_TYPEHASH, edgeId, data)));
        if (
            !edges[edgeId].to.creator.target.isValidSignatureNowCalldata(digest, signature)
                || _isUsed[keccak256(signature)]
        ) {
            revert Unauthorized();
        }
        _;
        _isUsed[keccak256(signature)] = true;
    }
```
The signature does not encode whether the signer intends to acknowledge or unacknowledge the edge. Additionally, the `checkSignature()` function ensures that each signature can only be used once in the `TitlesGraph` contract.
One issue with this approach is that malicious attackers can launch a denial-of-service (DoS) attack on the signature. They can front-run the `acknowledgeEdge()` call by calling `unacknowledgeEdge()` to spend the signature, causing `acknowledgeEdge()` to fail. Similarly, they can front-run the `unacknowledgeEdge()` call and spend the signature in `acknowledgeEdge()`.

Another issue with shared signatures is that a malicious relayer can save the signature for `acknowledgeEdge()` for later use in `unacknowledgeEdge()` or vice versa. For example, if Alice signs a signature to acknowledge an edge and sends it to Bob, expecting him to call the `acknowledgeEdge()` function on her behalf. Bob can claim he cannot call `acknowledgeEdge()` for her, and then Alice directly calls the without-signature version of [`acknowledgeEdge()`](https://github.com/sherlock-audit/2024-04-titles/blob/d7f60952df22da00b772db5d3a8272a988546089/wallflower-contract-v2/src/graph/TitlesGraph.sol#L103) to acknowledge the edge. Now that Bob has the signature, he can unacknowledge the edge if he wants, which compromises the integrity of the TitlesGraph.

> Additional audit information.
> 
> In addition to the security of funds, we would also like there to be focus on the sanctity of the data in the TitlesGraph and the permissioning around it (only the appropriate people/contracts can signal reference and acknowledgement of reference).

Since the sponsor highlighted `the sanctity of the data in the TitlesGraph` on the contest's [details](https://audits.sherlock.xyz/contests/326) page, I believe this issue is a high-severity one that the sponsor will pay attention to.

## Impact

1. Malicious attackers can prevent the signature from being correctly used.
2. Malicious relayers can save the signature for later use in the other direction to mess the edge's acknowledged state.

## Code Snippet
https://github.com/sherlock-audit/2024-04-titles/blob/d7f60952df22da00b772db5d3a8272a988546089/wallflower-contract-v2/src/graph/TitlesGraph.sol#L118-L124

https://github.com/sherlock-audit/2024-04-titles/blob/d7f60952df22da00b772db5d3a8272a988546089/wallflower-contract-v2/src/graph/TitlesGraph.sol#L146-L152

https://github.com/sherlock-audit/2024-04-titles/blob/d7f60952df22da00b772db5d3a8272a988546089/wallflower-contract-v2/src/graph/TitlesGraph.sol#L40-L50

## Tool used

Manual Review

## Recommendation
The `acknowledgeEdge()` and `unacknowledgeEdge()` functions should have distinct signatures. To achieve this, you can assign different `TYPEHASH`es to each function and verify them accordingly. Alternatively, you can encode the signer's intention (to acknowledge or not) in the digest of the signature and verify it accordingly.
It is also recommended to add a deadline for the signature.
